# Getting started with Terraform

## Deploying a Single server

- Terraform 코드는 _HCL(HashiCorp Configuration Language)_ 로, `.tf` 확장자를 사용한다.  
  선언적 언어이기 때문에 인프라의 원하는 상태를 코드로 나타내면 된다.

- Terraform 사용의 첫 번째 단계는 cloud provider에 대한 설정을 지정하는 것이다.

  ```tf
  provider "aws" {
  	region = "ap-northeast-2"
  }
  ```

- 위 파일은 사용할 cloud provider가 AWS이고, ap-northeast-2 region을 사용할 것임을 나타낸다.

- Provider 각각에 대해서 서버, 데이터베이스 등 다양한 종류의 _리소스_ 를 사용할 수 있다.  
  Terraform에서 리소스를 생성하는 일반적인 문법은 아래와 같다.

  ```tf
  resource "<PROVIDER>_<TYPE>" "<NAME>" {
  	[CONFIG ...]
  }
  ```

  - `<PROVIDER>`: aws 등 provider의 이름
  - `<TYPE>`: 해당 provider에 만들고 싶은 리소스의 종류(instance 등)
  - `<NAME>`: Terrraform 코드에서 해당 리소스를 참조하기 위한 이름

- 예를 들어 하나의 EC2 instance를 AWS에 배포하려면 아래처럼만 하면 된다.

  ```tf
  resource "aws_instance" "example" {
  	ami 		= "ami-0fb653ca2d3203ac1"
  	instance_type 	= "t2.micro"
  }
  ```

  - `aws_instance` 리소스는 다양한 인자를 지정할 수 있지만, 위에서는 필수로 필요한 ami와 instance type만 지정했다.

- 이제 위의 내용을 `main.tf`에 담고, 해당 파일이 있는 폴더로 이동해 `terraform init` 명령어를 실행해보자.  
  `terraform` 바이너리는 Terraform의 기본적인 기능을 포함하지만, cloud provider를 사용하기 위한 코드를 포함하진 않는다.  
  따라서 Terraform을 처음 사용할 때는 `terraform init`을 수행해 Terraform이 코드를 스캔하고, 어떤 provider를 사용하는지  
  파악한 후 해당 provider를 사용하기 위한 코드를 받을 수 있게끔 해야 한다.

- `terraform init`을 수행하면 다양한 파일들이 생겨나는데, 하나씩 살펴보자.

  - `.terraform` 폴더: Terraform이 사용할 provider의 코드가 저장되는 폴더
  - `.terraform.lock.hcl`: Terraform이 다운로드한 provider의 코드에 대한 버전 정보가 저장되는 파일

- 이제 provider 코드의 준비도 끝났으니, `terraform plan` 명령어를 실행해보자.

```
Terraform used the selected providers to generate the following execution plan. Resource actions are indicated with the following symbols:
  + create

Terraform will perform the following actions:

  # aws_instance.example will be created
  + resource "aws_instance" "example" {
      + ami                                  = "ami-0fb653ca2d3203ac1"
      + arn                                  = (known after apply)
      + associate_public_ip_address          = (known after apply)
      + availability_zone                    = (known after apply)
      + cpu_core_count                       = (known after apply)
      + cpu_threads_per_core                 = (known after apply)
      + disable_api_stop                     = (known after apply)
      + disable_api_termination              = (known after apply)
      + ebs_optimized                        = (known after apply)
      + get_password_data                    = false
      + host_id                              = (known after apply)
      + host_resource_group_arn              = (known after apply)
      + iam_instance_profile                 = (known after apply)
      + id                                   = (known after apply)
      + instance_initiated_shutdown_behavior = (known after apply)
      + instance_state                       = (known after apply)
      + instance_type                        = "t2.micro"
      + ipv6_address_count                   = (known after apply)
      (...)
    }

Plan: 1 to add, 0 to change, 0 to destroy.
```

- 위에서 볼 수 있듯이 `terraform plan` 명령은 `terraform apply`를 할 때 어떤 변경 사항이 인프라에 반영될지를 보여준다.  
  이렇게 `terraform plan`을 사용하면 코드에 대한 검증을 해볼 수 있다.  
  위 output의 경우 Terraform은 새로운 하나의 EC2 instance를 띄울 것임을 알 수 있다.

- 이제 `terraform apply`를 수행하면 새로운 EC2 instance가 생성된다.

- 이제 선언적 언어의 장점을 확인하기 위해 이 EC2 instance에 새로운 tag를 지정하고 싶다고 해보자.  
  아래처럼 `main.tf` 파일을 수정해보자.

  ```tf
  resource "aws_instance" "example" {
  ami           = "ami-0443a21cb1a8f238e"
  instance_type = "t2.micro"
  tags = {
    "Name" = "terraform-example"
    }
  }
  ```

- `terraform apply`를 수행하면 지정한 tag가 기존 instance에 적용된다.

- Terraform은 configuration file들로 생성한 리소스들의 상태를 추적하기 때문에, 기존에 이미 EC2 instance가 있음을 알 수 있다.

---

## Deploying a Single Web Server

- 이번에는 기존에 생성한 인스턴스에 아래 그림과 같이 web server를 실행해볼 것이다.

  ![picture 1](/images/TFRU_9.png)

- 실제로는 RoR, Django 등의 프레임워크를 사용해 웹 서버를 개발하겠지만, 여기서는 단순하게 가져가보자.  
  아래의 스크립트는 항상 "Hello, World"만을 반환하는 매우 간단한 웹 서버이다.

  ```sh
  #!/bin/bash
  echo "Hello, World" > index.html
  nohup busybox httpd -f -p 8080 &
  ```

- EC2가 실행될 때마다 위 스크립트를 실행하도록 하는 방법으로 EC2 user data configuration 기능을 활용할 수 있다.  
   그리고 이를 Terraform 파일에 아래처럼 지정할 수 있다.

  ```tf
  resource "aws_instance" "example" {
  ami           = "ami-0443a21cb1a8f238e"
  instance_type = "t2.micro"

  tags = {
    "Name" = "terraform-example"
  }

  user_data_replace_on_change = true

  user_data = <<-EOF
              #!/bin/bash
              echo "Hello, World" > index.html
              nohup busybox httpd -f -p 8080 &
              EOF
  }
  ```

- 위 코드에서 봐야할 가지 한 가지는 `<<-EOF`와 `EOF`이다. 이는 Terraform의 _heredoc_ 문법으로, `\n` 문자를 입력하지 않고도  
  여러 줄의 문자열을 입력할 수 있게 해준다.

- 다음으로 해당 EC2 instance가 8080 포트로 접근할 수 있도록 하기 위해 security group을 만들어보자.

  ```tf
  resource "aws_security_group" "instance" {
  name = "terraform-example-instance"
  ingress = [{
      cidr_blocks = ["0.0.0.0/0"]
      from_port   = 8080
      protocol    = "tcp"
      to_port     = 8080
    }]
  }
  ```

- 새롭게 생성한 security group을 EC2 instance에 연결해줘야 한다. 이는 `vpc_security_group_ids` 라는 인자를 통해  
  지정할 수 있는데, 적용하기 전 먼저 Terraform _expression_ 에 대해 살펴보자.

- Terraform의 모든 표현식(expression)은 반환값이 존재한다. 다양한 표현식 중 자주 쓰이는 것은 _참조(reference)_ 로,  
  다른 Terraform 코드의 실제 값을 참조하도록 해준다. 예를 들어, 위에서 security group의 ID를 참조하려면  
  _resource attribute reference_ 를 사용해야 하며, 아래의 문법을 가진다.

  - `<PROVIDER>_<TYPE>.<NAME>.<ATTRIBUTE>`

- 한 번 EC2 코드에 사용해보자.

  ```tf
  resource "aws_instance" "example" {
  	#..
  	vpc_security_group_ids = [aws_security_group.instance.id]
  }
  ```

- 이렇게 하나의 리소스를 다른 리소스에서 참조하게 되면 _implicit dependency_ 를 생성하게 된다.  
  Terraform은 이러한 의존성들을 parsing하고, dependency graph를 만들어내고, 자동으로 어떤 순서로 리소스를 생성해야 할지  
  결정한다. 예를 들어, EC2와 Security Group이 함께 있는 코드를 처음 `apply` 한다면, Terraform은 Security Group을  
  먼저 만들어야 함을 안다.

- 이러한 dependency graph는 `terraform graph` 명령어를 통해 확인할 수 있다.

---

## Deploying a Configurable Web Server

- 지금까지 작성한 코드에는 8080번 포트 번호가 중복되어 사용된다. 이는 DRY(Don't Repeat Yourslef) 원칙에 위배되고,  
  이렇게 하는 대신 이를 변수로 지정할 수 있다. 이를 _input variable_ 이라고 하며, 아래와 같은 문법으로 선언할 수 있다.

  ```tf
  variable "NAME" {
  	[CONFIG ...]
  }
  ```

- 변수 선언에는 아래와 같은 optional한 인자들을 지정할 수 있다.

  - `description`: 변수에 대한 설명
  - `default`: 변수를 전달하는 방법으로는 CLI에서 `-var` flag를 사용하거나, 파일로 전달(`-var-file`)하거나, 환경 변수를  
    사옹할 수도 있다. 만약 어떠한 값도 전달되지 않는다면, `default`에 지정된 값이 사용된다.
  - `type`: 타입 강제를 위해 사용할 수 있다. `string`, `number`, `bool`, `list`, `map`, `set`, `object`, `tuple`,  
    `any`가 있다. 항상 타입을 지정해 단순한 오류를 피하는 것이 좋다.
  - `validation`: 변수 값에 대해 custom 검증을 수행하도록 할 수 있다.
  - `sensitive`: 만약 이 인자를 true로 지정하면, `terraform apply` 또는 `terraform plan`을 수행했을 때 실제 값이  
    콘솔로 출력되지 않는다. 비밀번호, API Key 등에 유용하게 사용할 수 있다.

- 아래는 input variable의 단순한 예시이다.

  ```tf
  variable "number_example" {
  	description 	= "An example of a number variable in Terraform"
  	type 		= number
  	default 	= 42
  }
  ```

- 아래는 값이 `list`인 변수의 예시이다.

  ```tf
  variable "list_example" {
  	description 	= "An example of list in Terraform"
  	type 		= list
  	default 	= ["a", "b", "c"]
  }
  ```

- 아래와 같이 타입을 복합적으로 지정할 수도 있다.

  ```tf
  variable "list_numeric_example" {
  	description 	= "An example of a numeric list in Terraform"
  	type 		= list(number)
  	default 	= [1, 2, 3]
  }
  ```

- 그리고 아래는 모든 원소가 `string`이어야 하는 `map`의 예시이다.

  ```tf
  variable "map_example" {
  	description 	= "An example of a map in Terraform"
  	type 		= map(string)
  	default = {
  		key1 = "value1"
  		key2 = "value2"
  	}
  }
  ```

- 아래와 같이 더욱 복합적인 _structural type(구조적 타입)_ 을 `object` 타입을 이용해 사용할 수도 있다.

  ```tf
  variable "object_example" {
  	description = "An example of an object in Terraform"
  	type = object({
  		name 		= string
  		age 		= number
  		tags 		= list(string)
  		enabled 	= bool
  	})

  	default = {
  		name 		= "value1"
  		age 		= 42
  		tags 		= ["a", "b", "c"]
  		enabled 	= true
  	}
  }
  ```

- 만약 타입을 지정했는데, 잘못된 값이 입력되면 Terraform 명령을 수행할 때 오류가 발생하게 된다.

- 이제 다시 web server 예제로 돌아와서, 포트 번호를 담는 변수를 아래와 같이 선언해보자.

  ```tf
  variable "server_port" {
  	description 	= "The port the server will use for HTTP requests"
  	type 		= number
  }
  ```

- 위에서 생성한 `server_port`에는 기본값이 없음에 유의하자. 따라서 바로 `apply` 명령을 수행하면, Terraform이 해당  
  변수의 값을 입력하라고 콘솔에 알려줄 것이다. 만약 이렇게 입력하고 싶지 않다면, `terraform apply -var "server_port=8080`과  
  같이 `-var` flag를 사용해 변수를 전달하거나, `export TF_VAR_server_port=8080`을 수행해 환경 변수로 전달할 수도 있다.  
  물론 가장 편리한 방법은 default 값을 지정하는 것이다.

  ```tf
  variable "server_port" {
  	description 	= "The port the server will use for HTTP requests"
  	type 		= number
  	default 	= 8080
  }
  ```

- 이제 선언한 변수를 사용하는 방법을 알아보자. Terraform 코드로 선언한 변수를 다른 부분에서 사용하려면 아래의 _variable reference_  
  문법을 사용하면 된다.

  - `var.<VARIABLE_NAME>`

- 예를 들어 이전에 생성한 security group은 아래처럼 변경할 수 있을 것이다.

  ```tf
  resource "aws_security_group" "instance" {
    name = "terraform-example-instance"
    ingress = [{
      cidr_blocks = ["0.0.0.0/0"]
      from_port   = var.server_port
      protocol    = "tcp"
      to_port     = var.server_port,
      security_groups : [],
      self : false,
      description : "Example SG",
      prefix_list_ids : [],
      ipv6_cidr_blocks : [],
    }]
    egress = [{
      cidr_blocks = ["0.0.0.0/0"],
      from_port   = 0,
      protocol    = "all",
      to_port     = 0,
      security_groups : [],
      self : false,
      description : "Example SG",
      prefix_list_ids : [],
      ipv6_cidr_blocks : [],
    }]
  }
  ```

- heredoc syntax를 사용한 부분에서는 아래와 같이 `${}` syntax를 사용해 변수를 참조할 수도 있다.

  ```tf
  resource "aws_instance" "example" {
    ami           = "ami-0c55b159cbfafe1f0"
    instance_type = "t2.micro"

    user_data = <<-EOF
    #!/bin/bash
    echo "Hello, World!" > index.html
    nohup busybox httpd -f -p "${var.server_port}" &
    EOF
  }
  ```

- Input variable에 더해 Terraform은 _output variable_ 도 제공하며, 아래의 문법을 사용하면 된다.

  ```tf
  output "<NAME>" {
  	value = <VALUE>
  	[CONFIG ...]
  }
  ```

- `<NAME>`은 output variable의 이름이 되며, `<VALUE`는 출력하고 싶은 값으로, Terraform 표현식이다.  
  그리고 추가적으로 아래의 인자들을 지정할 수 있다.

  - `description`: output variable에 대한 설명
  - `sensitive`: output variable이 sensitive한 값인지 여부, true이면 output이 표시되지 않는다.
  - `depends_on`: 일반적으로 Terraform은 자동으로 의존성 그래프를 확인해 필요한 순서대로 실행하지만, 만약  
    그렇지 못하는 경우에는 `depends_on`을 명시적으로 지정해 의존성을 명시할 수 있다.

- 우리의 코드에서 아래와 같이 output variable을 선언해보자.

  ```tf
  output "public_ip" {
    value       = aws_instance.example.public_ip
    description = "The public IP address of the web server"
  }
  ```

- 그리고 `terraform apply`를 수행하면 아래와 같이 콘솔에 output variable의 값이 출력된다.

  ```sh
  > terraform apply
  Apply complete! Resources: 0 added, 0 changed, 0 destroyed.

  Outputs:

  public_ip = "3.135.213.108"
  ```

- apply하지 않고 output variable의 값만 확인하고 싶다면 `terraform output` 명령어를 사용하면 된다.  
  만약 output 변수가 많고, 그 중 특정 값만 출력해보고 싶다면 `terraform output <OUTPUT_VAR_NAME>`을 수행하면 된다.

  ```sh
  > terraform output public_ip
  "3.135.213.108"
  ```

---

## Deploying a Cluster of Web Servers

- 조금 더 현실적으로 가보자. 하나의 웹 서버만을 실행하는 것은 말도 안된다.

- AWS의 경우, ASG(Auto Scaling Group)을 활용하면 아래 그림과 같이 EC2 instance들의 클러스터를 완전히 자동으로  
  scaling해준다.

  ![picture 2](/images/TFRU_10.png)

- ASG를 만들기 위해 먼저 launch configuration을 만들어보자. Launch configuration은 ASG 내의 각 EC2 instance에 대한  
  설정 정보가 들어간다.

  ```tf
  resource "aws_launch_configuration" "example" {
    image_id        = "ami-0fb653ca2d3203ac1"
    instance_type   = "t2.micro"
    security_groups = [aws_security_group.instance.id]

    user_data = <<-EOF
                #!/bin/bash
                echo "Hello, World" > index.html
                nohup busybox httpd -f -p ${var.server_port} &
                EOF
  }
  ```

- 이제 ASG를 생성해보자.

  ```tf
  resource "aws_autoscaling_group" "example" {
    launch_configuration = aws_launch_configuration.example.name

    min_size = 2
    max_size = 10

    tag {
      key                 = "Name"
      value               = "terraform-asg-example"
      propagate_at_launch = true
    }
  }
  ```

- 이 ASG는 최소 2개, 최대 10개의 EC2 instance를 실행시킬 것이다.  
  여기서 문제가 하나 있는데, 바로 ASG가 launch configuration name을 참조해 사용한다는 것이다.  
  이것이 문제가 되는 이유는 launch configuration 자체가 immutable하기 때문에, 만약 launch configuration의 속성 중  
  하나라도 바꾸게되면 Terraform이 이를 수정하려고 할 것이기 때문이다. 일반적으로 리소스를 교체할 때 Terraform은 기존 리소스를  
  제거하고 새로운 리소스를 생성하겠지만, 위 코드에서 ASG는 여전히 기존 리소스(launch configuration)를 참조하고 있기 때문에  
  Terraform이 삭제할 수 없게 된다.

- 이를 해결하기 위해 _lifecycle_ setting을 활용할 수 있다. 모든 Terraform 리소스는 여러 가지의 생명 주기 관련 설정을  
  지정하고, 이는 리소스의 생성 방식, 갱신 또는 삭제 방식을 나타낸다. 특히 `create_before_destroy` lifecycle setting이  
  유용하게 사용되는데, 이 값이 true로 지정되면 Terraform은 새로운 리소스를 먼저 생성하고, 기존의 리소스를 삭제하게 된다.  
  기존 launch configuration 코드에 아래의 설정을 추가해주자.

  ```tf
  resource "aws_launch_configuration" "example" {
    #..
    lifecycle {
      create_before_destroy = true
    }
  }
  ```

- ASG를 설정하기 위해 한 가지 필수 인자가 있는데, 바로 `subnet_ids`이다.  
  여기에 지정하기 위한 subnet id들을 하드코딩할 수도 있겠지만, 더 좋은 방식은 AWS 계정으로부터 subnet id들을 _data source_ 를 활용해 읽어오는 것이다.

- Data source는 Terraform을 실행할 때마다 cloud provider로부터 가져올 수 있는 read-only 정보를 나타낸다.  
  Read-only이기 때문에 Terraform 코드에 새로운 data source를 추가하는 것은 인프라에 아무런 작용도 하지 않는다.  
  즉, data source는 cloud provider의 API를 활용해 Terraform 코드에서 필요한 값을 사용할 수 있게 해준다.

- AWS의 경우에는 VPC 정보, subnet, AMI ID, IP 주소 대역, 사용자의 신원 등의 data source를 지원한다.

- Data source를 사용하는 문법은 resource 문법과 매우 유사하다.

  ```tf
  data "<PROVIDER_TYPE>" "NAME" {
  	[CONFIG...]
  }
  ```

- 지금까지 작성한 코드에서 새로운 VPC를 생성하지 않고 default VPC를 사용했으므로 아래 코드를 사용해 default VPC의 정보를 가져오도록 해보자.

  ```tf
  data "aws_vpc" "default" {
    default = true
  }
  ```

- Data source 부분의 인자는 대부분 search filter로 사용된다. 예를 들어, 위 코드에서는 `aws_vpc`에서 `default = true`를  
  지정함으로써 default VPC만을 가져오게 되는 것이다.

- Data source에서 데이터를 사용하려면 `data.<PROVIDER>_<TYPE>.<NAME>.<ATTRIBUTE>` 형태로 사용하면 된다.  
  예를 들어 VPC ID를 가져오고 싶다면 `data.aws_vpc.default.id` 를 사용하면 된다.

- 아래와 같이 data source들을 결합해 사용할 수 있다.  
   아래 코드는 default VPC 내의 subnet들을 가지는 data source의 예시이다.

  ```tf
  data "aws_subnets" "default" {
    filter {
      name   = "vpc-id"
      values = [data.aws_vpc.default.id]
    }
  }
  ```

- 이제 마지막으로 ASG에 `subnet_ids(실제 key는 vpc_zone_identifier 이다.)`를 지정해주도록 하자.

  ```tf
  resource "aws_autoscaling_group" "example" {
    launch_configuration = aws_launch_configuration.example.name

    min_size = 2
    max_size = 10

    vpc_zone_identifier = data.aws_subnets.default.ids

    tag {
      key                 = "Name"
      value               = "terraform-asg-example"
      propagate_at_launch = true
    }
  }
  ```

---

## Deploying a Load Balancer

- 지금까지의 코드라면 ASG를 배포할 수는 있지만, ASG 내의 서버들에 접근할 수 있는 Load balancer는 없다.  
  아래 그림처럼 ALB를 구성해보자.

  ![picture 3](/images/TFRU_11.png)

- 아래처럼 ALB와 해당 ALB의 listener, ALB에 사용할 security group, 그리고 target group을 구성해보자.

  ```tf
  resource "aws_lb" "example" {
    name               = "terraform-asg-example"
    load_balancer_type = "application"
    subnets            = data.aws_subnets.default.ids
    security_groups    = [aws_security_group.alb.id]
  }

  resource "aws_lb_listener" "http" {
    load_balancer_arn = aws_lb.example.arn
    port = 80
    protocol = "HTTP"

    default_action {
      type = "fixed-response"

      fixed_response {
        content_type = "text/plain"
        message_body = "404: page not found"
        status_code  = 404
      }

    }
  }

  resource "aws_security_group" "alb" {
    name = "terraform-example-alb"

    ingress = [{
      cidr_blocks = ["0.0.0.0/0"]
      from_port = 80
      protocol = "tcp"
      to_port = 80,
      security_groups : [],
      self : false,
      description : "Example SG",
      prefix_list_ids : [],
      ipv6_cidr_blocks : [],
    }]
    egress = [{
      cidr_blocks = ["0.0.0.0/0"],
      from_port = 0,
      protocol = "all",
      to_port = 0,
      security_groups : [],
      self : false,
      description : "Example SG",
      prefix_list_ids : [],
      ipv6_cidr_blocks : [],
    }]
  }

  resource "aws_lb_target_group" "asg" {
    name = "terraform-asg-example"
    port = var.server_port
    protocol = "HTTP"
    vpc_id = data.aws_vpc.default.id

    health_check {
      path = "/"
      protocol = "HTTP"
      matcher = "200"
      interval = 15
      timeout = 3
      healthy_threshold = 2
      unhealthy_threshold = 2
    }
  }
  ```

- 다음으로 위에서 생성한 target group이 요청을 전달할 ASG를 알도록 하기 위해, ASG에 아래의 속성을 지정해주자.

  ```tf
  resource "aws_autoscaling_group" "example" {
    #..
    target_group_arns = [aws_lb_target_group.asg.arn]
    health_check_type = "ELB"
  }
  ```

- 마지막으로 listener에 target group과 전달 방식을 지정하는 listener rule을 작성해보자.

  ```tf
  resource "aws_lb_listener_rule" "http" {
    listener_arn = aws_lb_listener.http.arn
    priority     = 100

    condition {
      path_pattern {
        values = ["*"]
      }
    }

    action {
      type             = "forward"
      target_group_arn = aws_lb_target_group.asg.arn
    }
  }
  ```

- 위의 코드를 apply하면 ALB가 생성되고, 정상적으로 EC2 instance들에게 트래픽을 전달할 것이다.  
   마지막으로 ALB의 DNS name을 파악하기 위해 output variable을 만들어보자.

  ```tf
  output "alb_dns_name" {
    value       = aws_lb.example.dns_name
    description = "The domain name of the load balancer"
  }
  ```

- 이제 `terraform apply`를 하면 리소스들이 생성되며, 생성된 ALB의 dns name이 출력된다.

  ```sh
  > terraform apply
  Apply complete! Resources: 5 added, 1 changed, 0 destroyed.
  Outputs:
  alb_dns_name = "terraform-asg-example-822773707.us-east-2.elb.amazonaws.com"
  public_ip = "3.135.213.108"

  > curl terraform-asg-example-822773707.us-east-2.elb.amazonaws.com
  Hello, World
  ```

---

## Cleanup

- 이제 사용한 리소스들을 정리해보자. Terraform으로 관리되는 리소스는 `terraform destroy` 명령으로 모두 한 번에 제거할 수 있다.  
  이 명령은 취소가 불가능하기 때문에, 실행하면 Terraform이 정말 모든 것을 삭제할 것이냐고 묻게 된다.

---
