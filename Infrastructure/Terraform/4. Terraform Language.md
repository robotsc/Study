# Terraform Language

## HCL(Hashicorp Configuration Language) Syntax

- Terraform configuration is running using HCL, a human-friendly, readable, and writeable syntax, perfect for  
  codifying infrastructure requirements. HCL's configuration was created to have a more clearly visible and  
  defined structure when compared to other well-known configuration language such as JSON and YAML.

- At the top level, the HCL syntax comprises _stanzas_ or _blocks_ that define a variety of configurations  
  available to Terraform. Stanzas or blocks are comprised of key-value pairs.  
  Terraform accepts values of type string, number, boolean, list and map.  
  Single line comments start with hash(`#`), while multi-line comments use `/*` for opening and `*/` for closing.

- An interpolated variable reference is used within `${}`

```tf
tags = {
	Name = "Public Subent - ${element(var.availability_zones, count.index)}"
	Type = "public - ${var.project}"
}
```

- Single line strings are written in double quotes.

```tf
tags = {
	name = "Sangwoo-Terraform"
}
```

- Multi-line strings are specified using the heredoc format.  
  An opening EOF(end of file) character sequence is paired with a closing EOF character sequence.  
  In-between each line is considered part of the multi-line string. This multi-line string approach is often used  
  to capture scripts as used within the user-data attribute when bootstrapping EC2 instances.

```tf
user_data = <<EOF
#!/bin/bash
apt-get -y update
apt-get -y install nginx
service nginx start
echo fin v1.00!
EOF

tags = {
	name = "Sangwoo-Terraform"
}
```

- Maps are defined using curly braces, and are a collection of key-value pairs.  
  They are often used for creating variables that act as lookup tables.

```tf
variable "amis" {
	type = map
	default = {
		"us-east-2": "ami-123"
		"us-west-2": "ami-456"
	}
}
```

---

## Providers

- The Terraform Core program requires **at least one provider** to build anything.  
  You can manually configure which versions of a provider you would like to use. If you leave this option out,  
  Terraform will default to the latest available version of the provider.

```tf
terraform {
	required_providers {
		aws = {
			source = "hashicorp/aws"
			version = "-> 3.55.0"
		}
	}
}

provider "aws" {
	profile = "default"
	region = "us-east-1"
}
```

> Remember to initialize the current working directory using the `terraform init` command, which is required  
> before attempting to perform a `plan` or `apply`.

---

## Resources

- Let's look at the example Terraform code first.

```tf
resource "aws_vpc" "main" {
	cidr_block = "10.0.0.0/16"
	instance_tenancy = "default"

	tags = {
		Name = "Sangwoo"
	}
}

resource "aws_subnet" "public" {
	vpc_id = aws_vpc.main.id
	count = length(var.availability_zones)
	cidr_block = cidrsubnet(var.cidr_block, 8, count.index)
	availability_zone = element(var.availability_zones, count.index)

	map_public_ip_on_launch = true

	tags = {
		Name = "Public subnet - ${element(var.availability_zones, count.index)}"
		Type = "public"
	}
}
```

- The `resource` keyword is used to declare the type of resource you want to provision.

- In the example above, we are declaring two AWS resources: A VPC and a subnet.  
  Each resource is then configured with its required and optional attributes.

> In the example code, the subnet resource utilizes and sets the `count` attribute, which is considered a  
> meta-argument within Terraform. It allows you to create multiple versions of the resource it is declared within.

- In the example above, Terraform will create one subnet per AZ. The outcome of applying this Terraform configuration  
  will be an AWS VPC, which has public subnets deployed into each of the AZs for which the VPC spans across.  
  This type of syntax, although more abstract, is far more concise and compact when compared with hand-writing  
  each subnet individually.

### Resource Anatomy

- When declaring resources, the following layout is required.

```tf
resource "<type>" "<name>" {
	parameter = "foo"
	parameter2 = "bar"
	list = ["one", "two", "three"]
	block = {
		field1 = "foo"
		field2 = "bar"
	}
}

# Resource example 1
resource "aws_vpc" "main" {
	cidr_block = "10.0.0.0/16"

	tags = {
		name = "Sangwoo"
	}
}

# Resource Example 2
resource "aws_subnet" "subnet1" {
	vpc_id = aws_vpc.main.id
	cidr_block = "10.0.1.0/24"
	availability_zone = data.aws_availability_zones.available.names[0]

	tags = {
		Type = "public"
	}
}
```

- `<type>`: The type of the resource to be provisioned.

- In the two resource examples above, we are declaring types of aws_vpc and aws_subnet for an AWS BPC and an  
  AWS subnet, respectively. The resource `<name>` is an arbitrary name that you come up with that you can then later  
  use to refer to this instance of the resource.

- Every terraform resource, regardless of type, is structured exactly the same way.

```tf
resource "aws_instance" "numbercruncher" {
	ami = data.aws_ami.ubuntu.id
	instance_type = "c5.12xlarge"
	key_name = var.key_name
	subnet_id = var.subnet_id
	security_groups = [var.sg_id]
	associate_public_ip_address = true

	tags = {
		Name = "NumberCruncher"
		Owner = "Sangwoo"
	}
}
```

- This resource example demonstrates how to launch a single EC2 C5 instance type for the purposes of performing  
  number-crunching, etc. Here the `<type>` is set to be an aws_instance, which represents an EC2 instance.  
  The resource is then named "NumberCruncher" for lack of imagination.

---

## Data Sources

- Data sources are a way of querying an infrastructure provider for data about existing resources.  
  Data sources, when declared, can leverage one or several filters to narrow down the return data, to be more  
  specific about the requirement at hand.

> Data sources are constructs, used by Terraform to represent sources of information defined outside of Terraform.

```tf
data "aws_ami" "ubuntu" {
	most_recent = true

	filter {
		name = "name"
		values = ["ubuntu/images/hvm-ssd/ubuntu-foca1-20.04-amd64-server-*"]
	}

	owners = ["099720109477"] # Canonical
}

resource "aws_instance" "numbercruncher" {
	ami = data.aws_ami.ubuntu.id # Using data source
	instance_type = "c5.12xlarge"
	key_name = var.key_name
	subnet_id = var.subnet_id
	security_groups = [var.sg_id]
	associate_public_ip_address = true

	tags = {
		Name = "NumberCruncher"
	}
}
```

- In the example abve, a data source is declared to return AMI-IDs for all available Ubuntu 20.04 images for the  
  current AWS region. If more than one image is discovered, the most recent one will be returned based on the fact  
  that the most_recent attribute has been declared to be true.

- The Ubuntu data source, as seen above, is then later used within the numbercruncher AWS instance resource to  
  specify its AMI. Using this type of approach instead of hard-coding the actual AMI-ID directly within the  
  AWS instance resource future-proofs your Terraform templates.

- For example, consier the scenario of the Ubuntu 20.04 OS being overhauled or patched by chronicle, perhaps due  
  to a recently discovered security vulnerability. Having done this, they will likely also publish a new set of  
  updated AMIs.

- Now the next time you perform a `terraform plan` or `terraform apply` command, Terraform will detect that your  
  existing instance or instances, running the old AMI are now out of date, and can be relaunched with the newer  
  equivalent updated AMI. Or when launching a brand-new environment, you'll always be safeguarded by the fact that  
  the instance launched within it will be using the latest patched and up-to-date AMI.

```tf
data "aws_availability_zone" "available" {
	state = "available"
}

resource "aws_vpc" "main" {
	cidr_block = "10.0.0.0/16"

	tags = {
		Name = "Sangwoo"
	}
}

resource "aws_subnet" "subnet1" {
	vpc_id = aws_vpc.main.id
	cidr_block = "10.0.1.0/24"
	availability_zone = data.aws_availability_zones.available.names[0] # Using data source

	tags = {
		Name = "subnet1"
		Type = "public"
	}
}
```

- In the example above, infromation about all available AZs for the current AWS region is queried for.  
  The AZ data source is then referenced within the subnet resource being declared. Here, the availability_zone  
  attribute takes on the first value contained within the AZ's data source. Taking this approach helps to keep  
  our Terraform templates generalized such that they can be reused easily across different AWS regions.

---

## Variables

- Variables are another technique to assist in keeping your Terraform configurations generalized and reusable  
  for multiple requirements. The idiomatic practice is to store variables in a file named `variables.tf`.

- Variables can have default settings, and if the default is omitted, the user will be prompted to enter a value.

```tf
# variables.tf

variable "instance_type" {}
variable "key_name" {}
variable "subnet_id" {}
variable "sg_id" {}

# main.tf
resource "aws_instance" "bastion" {
	ami = data.aws_ami.ubuntu.id
	instance_type = var.instance_type
	key_name = var.key_name
	subnet_id = var.subnet_id
	security_groups = [var.sg_id]
	associate_public_ip_address = true

	tags = {
		Name = "Sangwoo,Compute1"
	}
}
```

- In the example above, we are declaring the variables that we intend to use, but haven't declared any default values.

- The declared variables can then be referenced from within the `main.tf` and for the same meta elsewhere in all  
  other `*.tf` files in the current directory.

- It is importatn to understand that **Terraform provides several ways in which you can see and override the default**  
  **value for any and all decalred variables.** If multiple approaches are used together, then Terraform follows a  
  defined procedure in terms of which ones get used first.  
  Let's review each of the approaches from the highest to the lowest priority.

### 1. Leveraging command line variable flags.

```sh
terraform apply -var="instance_type=t3.micro" -var="key_name=sangwoo-key"
```

- If default values are defined on the command line, then these will have the highest priority.

### 2. Defining variables within `terraform.tfvars` file.

```sh
terraform apply -var-file="sangwoo.prod.tfvars"
terraform apply -var-file="sangwoo.dev.tfvars"
```

- This option allows you to define your variable's values within a `terraform.tfvars` file.  
  If this is detected unavailable, it will be automatically used.  
  If required, you can have multiple distinctly named versions of the `*.tfvars` file. When you do so, you must  
  declare which one is being used via the `-var-file` parameter. This approach is perhaps useful to alter the  
  infrastructure provisioning process for different environments, etc.

### 3. Using environment variables.

```sh
export TF_VAR_instance_type=t3.micro
export TF_VAR_key_name=sangwoo-key
```

- Within the shell or terminal session from which the Terraform CLI is being used, you can see environment  
  variables named with the following naming strategy. Capitals `TF_VAR_` followed by the actual name of the  
  variable itself, and then assign it with a value.

### 4. Default Config - Default value in `variables.tf` or `terraform app

- Uses the default values stored against the declared variables within the `variables.tf` file.

### 5. User manual entry - if not specified, prompt the user for entry.

```sh
terraform apply
var.instance_type
	Enter a value: t3.micro

var.key_name
	Enter a value: sangwoo-key
```

- You will be prompted to supply a value at runtime within the terminal during the Terraform execution.

---

## Outputs

> Outputs: Used to explicitly return values assigned to resources configured within the corresponding  
> `main.tf` file - once provisioned.

```tf
# main.tf

resource "aws_instance" "bastion" {
	ami = data.aws_ami.ubuntu.id
	instance_type = var.instance_type
	key_name = var.key_name
	subnet_id = var.subnet_id
	security_groups = [var.sg_id]
	associate_public_ip_address = true

	tags = {
		Name = "Bastion"
	}
}

# outputs.tf

output "public_ip" {
	description = "public ip address"
	value = aws_instance.bastion.public_ip
}
```

- Output files are like the return values of a Terraform module. The idiomatic practice is to store outputs  
  in a file named `outputs.tf`.

- Primarily, outputs are used for the following two purposes:

  - (1) Root module uses outputs to print out values in the terminal for your convenience. In the example above,  
    the public IP output would print out the AWS EC2 assigned public IP address to the terminal once the provisioning  
    has completed.

  - (2) A child module can use outputs to export a set of values which are required and used elsewhere within its  
    parent module. From here, the parent module can then later pass these values as inputs to other child modules.

---
