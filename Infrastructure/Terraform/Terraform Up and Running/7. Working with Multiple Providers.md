# Working with Multiple Providers

- 지금까지 이 코드의 예제들은 단 하나의 `provider` block만을 가지고 있었다.

```tf
provider "aws" {
  region = "us-east-2"
}
```

- 위의 `provider` block은 하나의 AWS 계정으로 하나의 region에 대해서만 사용이 가능하다. 이에 대해 아래의 의문점을 가질 수 있다.

  - 여러 개의 region에 배포하려면 어떻게 해야할까?
  - 여러 개의 AWS 계정을 사용해 배포하려면 어떻게 해야할까?
  - Azure, GCP 등의 다른 cloud provider들도 사용해야 하면 어떻게 해야할까?

- 위의 질문들에 대한 답을 알아보기 위해, Terraform provider와 관련해 아래의 내용을 다뤄보자.

  - Working with one provider
  - Working with multiple copies of the same provider
  - Working with multiple different providers

## Working with one provider

- 지금까지 사용한 대로 `provider` block을 사용하면 하나의 기본적인 provider를 사용해 간단한 예제들을 수행할 수 있다.  
  하지만 하나 이상의 region, account, cloud 등을 사용하려면 더 복잡한 구성이 필요하다.

- 우선 단일 provider를 사용하는 경우에 대해 살펴보자.

### What is a provider?

- 이 책의 2장에서는 provider를 Terraform이 상호작용하는 _platform_ 이라고 표현했으며, 여기에는 AWS, Azure, Googe Cloud 등이  
  있다고 했다. 그래서 Terraform이 이 플랫폼들과 어떻게 상호작용 하는 것일까?

- 내부적으로 Terraform은 두 가지 부분으로 구성된다.

  - **Core** : Core는 `terraform` 바이너리로, CLI, parser, HCL interpreter 등 모든 플랫폼이 사용하는 Terraform의  
    기본적인 기능들을 담고 있다. Go로 작성되어 있으며, 오픈소스로 공개되어 있다.

  - **Providers** : Terraform provider는 Terraform core를 위한 _plugin_ 이다. 각 플러그인은 특정 인터페이스를  
    구현하기 위해 Go로 작성되어 있으며, Terraform core는 플러그인을 설치하고 실행시키는 방법을 알고 있다.

    ![picture 1](/images/TFRU_22.png)

    - 각 provider는 특정한 prefix를 가지며, 하나 이상의 리소스와 data source를 노출한다. 예를 들어, AWS provider는  
      `aws_` prefix를 가지며, Azure의 경우에는 `azurerm_` prefix를 가지고 있다.

### How do you install providers?

- AWS, Azure, GCP 등의 공식 Terraform provider의 경우, `provider` block을 코드에 추가하는 것으로 충분하다.

```tf
provider "aws" {
  region = "us-east-2"
}
```

- 이 상태에서 `terraform init`을 수행하는 순간, Terraform은 해당 provider를 위한 코드를 다운로드하기 시작한다.  
  여기서 Terraform이 어떤 provider가 필요한지, 어떤 버전이 필요한지, 어디서로부터 다운로드해야 할지 등에 대한 세부 사항을  
  모두 파악할 필요는 없다. 하지만 상용 환경의 경우, Terraform이 provider를 설치하는 과정을 조작해야 하는 경우가 있는데,  
  이는 `required_providers` block을 통해 지정할 수 있다.

```tf
terraform {
  required_providers {
    <LOCAL_NAME> = {
      source = "<URL>"
      version = "<VERSION>"
    }
  }
}
```

- `LOCAL_NAME`: 모듈 내에서 provider를 사용할 _local name_ 이다. 각 provider에는 고유한 이름을 지정해줘야 하며,  
  이 이름을 가지고 `provider` block에서 사용할 수 있다. 대부분의 경우, 해당 provider의 _preferred local name_ 을  
  사용하는 것이 좋은데, 예를 들어 AWS의 경우에는 `aws`를 사용하면 된다.

- `URL`: `URL`은 Terraform이 provider를 사용하기 위한 코드를 다운받을 주소를 의미한다.  
  `[<HOSTNAME>/]<NAMESPACE>/<TYPE>`의 형식을 가지며 `HOSTNAME`은 provider를 배포하는 Terraform Registry의  
  hostname, `NAMESPACE`는 organizational namespace, 그리고 `TYPE`는 해당 provider가 관리하는 플랫폼을 말한다.  
  예를 들어 AWS provider의 전체 URL은 `registry.terraform.io/hashicorp/aws`이다.  
  여기서 `HOSTNAME`은 optional한 값으로, 만약 지정하지 않는다면 기본적으로 public Terraform Registry 주소가 들어가게 된다.

- `VERSION`: 버전에 대한 규악으로, 특정 버전으로 지정하거나, `> 4.0`, `< 4.3`과 같이 범위를 지정할 수도 있다.

```tf
terraform {
  required_providers {
    aws = {
      source = "hashicorp/aws"
      version = "~> 4.0"
    }
  }
}
```

- 만약 `foo`라는 `provider` block을 코드에 작성하고 `apply`를 수행하면, Terraform은 아래의 작업들을 자동으로 수행한다.

  - `foo`라는 이름을 가진 provider를 `HOSTNAME`이 public Terraform registry라고 가정하고 찾아 다운로드하려 한다.  
    그리고 이 URL이 유효하다면 가장 최신 버전을 다운받는다.

- 만약 `hashicorp` namespace가 아닌 Datadog, Cloudflare 등의 provider를 사용하고 싶거나 provider의 버전을 지정해 사용하고  
  싶다면 이 내용을 `required_providers` block에 지정해야 한다.

> 이후에 보겠지만, `required_providers` block을 항상 사용하는 것이 좋다.

### How to use providers?

- 이제 기존 코드를 아래처럼 바꿔보자.

```tf
terraform {
  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = "~> 4.0"
    }
  }
}

provider "aws" {
  region = "us-east-2"
}
```

- 지금까진 위에서 `provider` block에 region 설정만 해주었는데, 각 provider마다 다양한 설정 가능한 값들이 있다.  
  따라서 항상 해당 provider의 문서를 잘 찾아보도록 하자.

---

## Working with multiple copies of the same provider

- 같은 provider에 대해 아래의 작업들을 하는 방법을 살펴보자.

  - Working with multiple AWS regions
  - Working with multiple AWS accounts
  - Creating modules that can work with multiple providers

### Working with multiple AWS regions

- Terraform provider를 사용할 때, 일반적으로 하나의 region에 배포하도록 설정한다.  
  하지만 두 개 이상의 region에 대해 작업하고 싶으면 어떻게 해야할까? 예를 들어, 일부는 us-east-2에, 다른 일부는 us-west-1에  
  배포하려면 어떻게 해야할까?

- 바로 드는 생각은 아래처럼 2개의 `provider` 설정 코드를 지정하는 것이다.

```tf
provider "aws" {
  region = "us-east-2"
}

provider "aws" {
  region = "us-west-1"
}
```

- 하지만 이렇게 하면 새로운 문제가 생긴다. 어떤 리소스에 어떤 `provider` 설정이 적용될 것인지를 어떻게 정의할 것인가?  
  먼저 data source를 살펴보자. 아래처럼 현재 AWS region을 반환하는 2개의 `aws_region` data source가 있다고 해보자.

```tf
data "aws_region" "region_1" {}
data "aws_region" "region_2" {}
```

- 여기서 `region_1`이 us-east-2 provider로, `region_2`가 us-west-1 provider로 연결되도록 어떻게 해야할까?  
  정답은 각 provider에 alias를 추가하는 것이다.

```tf
provider "aws" {
  region = "us-east-2"
  alias  = "region_1"
}

provider "aws" {
  region = "us-west-1"
  alias  = "region_2"
}
```

- _alias_ 는 `provider`에 대한 별칭으로, 명시적으로 리소스, data source, 그리고 모듈을 전달해 해당 provider의 설정이 적용되도록  
  할 수 있다. 위의 `aws_region` data source가 특정 provider를 사용하도록 하기 위해 아래처럼 `provider` parameter를  
  설정해야 한다.

```tf
data "aws_region" "region_1" {
  provider = aws.region_1
}

data "aws_region" "region_2" {
  provider = aws.region_2
}
```

- 이제 아래처럼 output을 지정해보자.

```tf
output "region_1" {
  value       = data.aws_region.region_1.name
  description = "The name of the first region"
}

output "region_2" {
  value       = data.aws_region.region_2.name
  description = "The name of the second region"
}
```

- `apply` 결과는 아래와 같다.

```
Outputs:

region_1 = "us-east-2"
region_2 = "us-west-1"
```

- 이제 원하는 리소스에 원하는 region을 설정할 수 있게 되었다.  
  서로 다른 ami를 가진 EC2 instance를 각 region에 하나씩 배포하는 코드를 보자. 아래와 같다.

```tf
resource "aws_instance" "region_1" {
  provider      = aws.region_1
  ami           = "ami-0fb653ca2d3203ac1"
  instance_type = "t2.micro"
}

resource "aws_instance" "region_2" {
  provider      = aws.region_2
  ami           = "ami-01f87c43e618bf8f0"
  instance_type = "t2.micro"
}
```

- 위 코드에서 각 `aws_instance` 리소스가 `provider`를 설정해 알맞은 region에 배포되도록 설정한다는 점에 유의하자.  
  또한 AMI ID는 region 단위로 고유하기 때문에 서로 다른 값을 사용한다. 예를 들어, Ubuntu 20.04의 us-east-2 AMI ID와  
  us-west-1 AMI ID는 서로 다르다. 이렇게 ami를 직접 지정하는 대신, `aws_ami` data source를 사용해 필터를 지정해  
  각 region에 맞는 AMI ID를 자동으로 가져오도록 수정해보자.

```tf
data "aws_ami" "ubuntu_region_1" {
  provider = aws.region_1

  most_recent = true
  owners      = ["099720109477"] # Canonical

  filter {
    name   = "name"
    values = ["ubuntu/images/hvm-ssd/ubuntu-focal-20.04-amd64-server-*"]
  }
}

data "aws_ami" "ubuntu_region_2" {
  provider = aws.region_2

  most_recent = true
  owners      = ["099720109477"] # Canonical

  filter {
    name   = "name"
    values = ["ubuntu/images/hvm-ssd/ubuntu-focal-20.04-amd64-server-*"]
  }
}

resource "aws_instance" "region_1" {
  provider      = aws.region_1
  ami           = data.aws_ami.ubuntu_region_1.id
  instance_type = "t2.micro"
}

resource "aws_instance" "region_2" {
  provider      = aws.region_2
  ami           = data.aws_ami.ubuntu_region_2.id
  instance_type = "t2.micro"
}
```

- 이제 어떠한 region에 대패 배포하더라도 자동으로 Ubuntu를 위한 알맞은 AMI ID를 가져와 사용할 수 있다.  
  이 EC2 instance들이 알맞은 region에 배포되는지를 확인하기 위해 아래처럼 output variable을 몇 개 선언해보자.

```tf
output "instance_region_1_az" {
  value       = aws_instance.region_1.availability_zone
  description = "The AZ where the instance in the first region is deployed"
}

output "instance_region_2_az" {
  value       = aws_instance.region_2.availability_zone
  description = "The AZ where the instance in the second region is deployed"
}
```

- 다음으로 이전에 봤던 `stage/data-stores/mysql`의 코드를 보자. 단일 MySQL instance를 배포하게 된다.

```tf
resource "aws_db_instance" "example" {
  identifier_prefix   = "terraform-up-and-running"
  engine              = "mysql"
  allocated_storage   = 10
  instance_class      = "db.t2.micro"
  skip_final_snapshot = true
  db_name             = "example_database"
  username            = var.db_username
  password            = var.db_password
}
```

- 상용 환경에서는 단일 데이터베이스가 SPOF가 될 수 있으므로, Amazon RDS가 제공하는 _replication_ 기능을 활용해보자.  
  이를 위한 모듈을 새로 만들어보자. 모듈을 `modules/data-stores/mysql`에 만들고, 아래의 두 가지 input variable을  
  선언해보자.

```tf
variable "backup_retention_period" {
  description = "Days to retain backups. Must be > 0 to enable replication."
  type        = number
  default     = null
}

variable "replicate_source_db" {
  description = "If specified, replicate the RDS database at the given ARN."
  type        = string
  default     = null
}
```

- 곧 보겠지만, primary database에 `backup_retention_period`가 사용되어 replication이 활성화되도록 할 것이며,  
  secondary database에 `replicate_source_db`를 지정해 replica로써 동작하도록 할 것이다.  
  우선 `aws_db_instance` 리소스를 아래처럼 수정하자.

  - (1) `backup_retention_period`, `replicate_source_db` variable을 `aws_db_instance` 리소스에 지정한다.
  - (2) 데이터베이스가 replica인 경우 AWS는 `engine`, `db_name`, `username`, `password`를 지정할 수 없도록 하기에  
    관련 설정들을 제거한다.

- 수정 후의 코드는 아래와 같다.

```tf
resource "aws_db_instance" "example" {
  identifier_prefix   = "terraform-up-and-running"
  allocated_storage   = 10
  instance_class      = "db.t2.micro"
  skip_final_snapshot = true

  # Enable backups
  backup_retention_period = var.backup_retention_period

  # If specified, this DB will be a replica
  replicate_source_db = var.replicate_source_db

  # Only set these params if replicate_source_db is not set
  engine   = var.replicate_source_db == null ? "mysql" : null
  db_name  = var.replicate_source_db == null ? var.db_name : null
  username = var.replicate_source_db == null ? var.db_username : null
  password = var.replicate_source_db == null ? var.db_password : null
}
```

- `replicate_source_db`를 사용하려면 다른 RDS 인스턴스의 ARN을 알아야 한다. 따라서 이를 output variable로 지정해주자.

```tf
output "arn" {
  value       = aws_db_instance.example.arn
  description = "The ARN of the database"
}
```

- 한 가지 더, 모듈이 AWS provider를 사용할 것이고, 어떤 버전을 사용할 것인지를 지정하자.

```tf
terraform {
  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = "~> 4.0"
    }
  }
}
```

- 이후이 여러 region을 사용할 때 위 설정이 왜 중요한지 볼 것이다.

- 이제 MySQL의 primary, replica를 production 환경에 배포해보자.  
  먼저 `live/prod/data-stores/mysql/variables.tf`에 아래의 두 개 input variable을 정의하자.

```tf
variable "db_username" {
  description = "The username for the database"
  type        = string
  sensitive   = true
}

variable "db_password" {
  description = "The password for the database"
  type        = string
  sensitive   = true
}
```

- 그리고 `live/prod/data-stores/mysql/main.tf`에 primary를 설정하는 `mysql` module을 정의하자.

```tf
module "mysql_primary" {
  source = "../../../../modules/data-stores/mysql"

  db_name     = "prod_db"
  db_username = var.db_username
  db_password = var.db_password

  # Must be enabled to support replication
  backup_retention_period = 1
}
```

- 다음으로 두 번째 `mysql` module을 정의한다. 이 모듈은 secondary를 위한 것이다.

```tf
module "mysql_replica" {
  source = "../../../../modules/data-stores/mysql"

  # Make this a replica of the primary
  replicate_source_db = module.mysql_replica.arn
}
```

- 이제 primary와 replica를 서로 다른 region에 배포하기 위한 설정만 남았다.  
  이전에 했던 것처럼 `provider` block에 alias를 추가해 정의해보자.

```tf
provider "aws" {
	region = "us-east-2"
	alias = "primary"
}

provider "aws" {
	region = "us-west-1"
	alias = "replica"
}
```

- 그리고 각 module에 `providers`를 지정하자.

```tf
module "mysql_primary" {
  source = "../../../../modules/data-stores/mysql"

  providers = {
    aws = aws.primary
  }
  db_name     = "prod_db"
  db_username = var.db_username
  db_password = var.db_password

  # Must be enabled to support replication
  backup_retention_period = 1
}

module "mysql_replica" {
  source = "../../../../modules/data-stores/mysql"

  providers = {
    aws = aws.replica
  }

  # Make this a replica of the primary
  replicate_source_db = module.mysql_replica.arn
}
```

- 위 코드에서 유의할 사항이 한 가지 있다. 모듈의 인자 중 `providers`(복수형)은 map 형식의 인자를 받으며,  
  리소스, data source가 사용하는 `provider`(단수형)은 하나의 값만을 받을 수 있다. 이는 각 리소스와 data source는  
  정확히 하나의 provider를 사용해 배포하지만, 모듈은 여러 provider들을 사용할 수 있기 때문이다.  
  `providers` map의 key는 모듈에 정의된 `required_providers`의 key와 정확히 일치해야 한다.  
  이 이유가 `required_providers`를 모듈 코드에 정의한 이유이다.

- 이제 아래처럼 output variable들을 만들고, apply 해보자.

```tf
output "primary_address" {
  value       = module.mysql_primary.address
  description = "Connect to the primary database at this endpoint"
}

output "primary_port" {
  value       = module.mysql_primary.port
  description = "The port the primary database is listening on"
}

output "primary_arn" {
  value       = module.mysql_primary.arn
  description = "The ARN of the primary database"
}

output "replica_address" {
  value       = module.mysql_replica.address
  description = "Connect to the replica database at this endpoint"
}

output "replica_port" {
  value       = module.mysql_replica.port
  description = "The port the replica database is listening on"
}

output "replica_arn" {
  value       = module.mysql_replica.arn
  description = "The ARN of the replica database"
}
```

### Working with multiple AWS accounts

- AWS Organization을 사용해 여러 개의 AWS 계정을 사용하는 환경을 가정해보자.  
  어떤 계정은 staging 환경에만, 그리고 어떤 환경은 production 환경에만 접근할 수 있도록 권한이 분리되어 있을 것이다.  
  Organization을 생성한 계정을 parent, 그리고 organization에서 생성한 계정을 child라고 하자.

- 두 개의 AWS 계정을 사용하려면 두 개 이상의 region을 사용하려 했던 때와 마찬가지로 `provider` block이 각각을 위해  
  존재해야 하며, alias도 지정해줘야 한다.

```tf
provider "aws" {
  region = "us-east-2"
  alias  = "parent"
}

provider "aws" {
  region = "us-east-2"
  alias  = "child"

  assume_role {
    role_arn = "arn:aws:iam::123412341234:role/OrganizationAccountAccessRole"
  }
}
```

- 위 코드에서 child는 인증을 위해 IAM Role을 assume하도록 했다.

- 위 코드가 동작하는지 확인해보기 위해 두 개의 `aws_caller_identity` data source를 만들어 각각 다른 provider를 사용하도록 해보자.

```tf
# variables.tf
data "aws_caller_identity" "parent" {
  provider = aws.parent
}

data "aws_caller_identity" "child" {
  provider = aws.child
}

# outputs.tf
output "parent_account_id" {
  value       = data.aws_caller_identity.parent.account_id
  description = "The ID of the parent AWS account"
}

output "child_account_id" {
  value       = data.aws_caller_identity.child.account_id
  description = "The ID of the child AWS account"
}
```

- `apply`를 수행해보고 output을 보면 두 provider의 계정 정보가 다름을 확인할 수 있다.

### Creating modules that can work with multiple providers

- Terraform module은 크게 두 가지로 분류된다.

  - **Reusable modules** : 직접적으로 배포가 가능하지만 다른 모듈, 리소스, data source와 조합해 사용할 수도 있는 low-level 모듈이다.
  - **Root modules** : 여러 개의 reusable module들을 하나의 단위로 조합해 `apply`를 수행하는 주체로, high-level 모듈이다.

- 지금까지 봤던 multi-provider 예제 코드들은 `provider` block들을 root module 쪽에 정의한다.  
  그렇다면 여러 개의 provider를 사용하는 module을 만들고 싶다면 어떻게 해야할까?

- 위에서 작성한 multi account를 위한 코드를 아래처럼 module로써 사용해보자.

```tf
module "multi_account_example" {
  source = "../../../modules/multi-account"
}
```

- 여기서 `apply`를 수행하면 동작은 하지만, 문제점이 있다. 바로 `provider` 관련 설정이 모듈에 의해 감춰진다는 것이다.  
  이렇게 `provider`를 reusable module 내에 정의하는 것은 아래의 문제점들이 있다.

  - **Configuration problems** : `provider` block이 reusable module에 감춰져 있다면, 해당 모듈이 `provider`에 대한  
    설정 및 조작을 책임지게 된다. 예를 들어, 현재 모듈 코드에 하드코딩되어 있는 IAM Role ARN과 region이 이에 해당된다.  
    물론 이들을 input variable로 노출시켜 받아오도록 할 수 있지만, AWS provider의 경우에만 해도 50개가 넘는 설정 옵션들이 있다.  
    그리고 이 옵션들의 대다수는 AWS와 인증하는 방식, 사용할 계정, region 등을 담기 때문에 사용자에게 중요한 정보이다.  
    이렇게 수많은 값들을 노출시키는 것은 모듈 사용을 매우 복잡하게 하고, 유지보수가 어렵게 만든다.

  - **Duplication problems** : 50개의 설정을 모듈로 노출시켜도, 대다수의 사용자들은 다양한 모듈을 조합해 사용하기 때문에  
    사용자들은 같은 설정을 여러 번 하게 된다.

  - **Performance problems** : `provider` block을 코드에 추가할 때마다, Terraform은 해당 provider의 작업을 수행하기  
    위한 새로운 프로세스를 띄우며, RPC로 상호작용한다. 따라서 `provider`들이 점점 많아지면 성능 문제가 발생하게 될 것이다.  
    더 나아가 이는 네트워크 오버로드를 발생시켜 API 장애를 야기해 `apply`가 실패하게 할 수도 있다.

- 위와 같은 이유들로 reusable module들에는 `provider` block을 정의하지 않는 것이 좋다.  
  그렇다면 여러 개의 provider를 사용하는 모듈을 어떻게 만들어야 할까?

- 해답은 _configuration alias_ 를 사용하는 것이다. 이는 이전에 봤던 provider alias와 매우 유사하다.  
  유일한 차이점은 `provider` block이 아니라 `required_providers` block에 정의된다는 것이다.

```tf
# modules/multi-account/main.tf, 즉 reusable module의 코드
terraform {
  required_providers {
    aws = {
      source                = "hashicorp/aws"
      version               = "~> 4.0"
      configuration_aliases = [aws.parent, aws.child]
    }
  }
}
```

- Reusable module에서 위처럼 `required_providers`에 alias를 지정해도 아래처럼 이전과 같이 가져와 사용할 수 있다.

```tf
data "aws_caller_identity" "parent" {
  provider = aws.parent
}

data "aws_caller_identity" "child" {
  provider = aws.child
}
```

- 일반적인 provider alias와 configuration alias의 차이점은 configuration alias는 직접 아무런 provider도 생성하지  
  않는다는 것이다. 대신, 모듈의 사용자가 명시적으로 각 configuration alias에 대한 provider를 지정해야 한다.

```tf
# reusable module을 사용하는 쪽의 코드
provider "aws" {
  region = "us-east-2"
  alias  = "parent"
}

provider "aws" {
  region = "us-east-2"
  alias  = "child"

  assume_role {
    role_arn = "arn:aws:iam::123412341234:role/OrganizationAccountAccessRole"
  }
}

module "multi_account_example" {
  source = "../../../modules/multi-account"

  providers = {
    aws.parent = aws.parent
    aws.child  = aws.child
  }
}
```

- `providers` map의 각 key는 module의 configuration alias와 정확히 일치해야 한다.

---
