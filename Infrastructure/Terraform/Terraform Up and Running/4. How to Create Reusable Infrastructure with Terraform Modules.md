# How to Create Reusable Infrastructure with Terraform Modules

- 이전 장에서 배포했던 환경을 아래 그림과 같이 staging, 그리고 production의 두 가지 환경에 동일하게 배포하고 싶다고 해보자.

  ![picture 1](/images/TFRU_16.png)

- production 환경에 staging과 동일한 환경을 코드의 복사, 붙여넣기 없이 어떻게 동일하게 구성할 수 있을까?

- Ruby와 같은 일반적인 프로그래밍 언어의 경우, 동일한 코드를 다른 곳으로 복사 붙여넣기 하는 대신 해당 부분을 함수로 빼내어  
  함수 호출로 동일한 코드를 재사용할 수 있다.

  ```rb
  def example_function()
    puts "Hello, World"
  end
  ```

- 반면 Terraform의 경우, 반복해 사용할 코드를 _Terraform module_ 내에 넣어놓고, 여러 곳에서 해당 모듈을 불러와 사용하는 것으로  
  코드의 재사용을 가능하게 한다. 아래 그림과 같다.

  ![picture 2](/images/TFRU_17.png)

- 이 기능은 꽤나 획기적이다. 모듈은 재사용성이 좋고, 유지보수하기 쉬우며, 테스트하기 쉬운 Terraform 코드를 작성하기 위한 핵심 요소이다.  
  한 번 쓰기 시작하면 다시 돌아가지 못할 것이다. 실제로 모든 것을 모듈로 만들어나갈 수 있다.

## Module Basics

- Terraform module은 매우 단순한데, 특정 폴더 하위에 있는 Terraform configuration file들의 집합은 모두 module이다.  
  지금까지 작성했던 Terraform code들은 모두 모듈이다. 그리고 `terraform apply`를 할 때 각 폴더에 들어가서 실행했는데,  
  이런 모듈을 _root module_ 이라고 부른다. 모듈이 어떤 효과를 제공하는지 느끼려면 재사용성 가능한 모듈을 만들어봐야 한다.

- 아래 그림처럼 기존에 `stage/services/webserver-cluster` 하위에 있던 모든 파일들을 `modules/services/webserver-cluster`의  
  하위로 이동시켜보자.

  ![picture 3](/images/TFRU_18.png)

- 이제 위에서 생성한 module을 staging 환경에서 사용할 수 있다.  
  모듈을 사용하는 문법은 아래와 같다.

  ```tf
  module "<NAME>" {
    source = "<SOURCE>"
    [CONFIG...]
  }
  ```

  - `<NAME>`: Terraform code에서 해당 모듈을 사용하기 위한 모듈 이름
  - `<SOURCE>`: 모듈 코드가 있는 경로

- 먼저 `stage/services/webserver-cluster` 하위에 아래의 내용을 담은 `main.tf` 파일을 생성해보자.

```tf
provider "aws" {
  region = "us-east-2"
}

module "webserver_cluster" {
  source = "../../../modules/services/webserver-cluster"
}
```

- 이처럼 `prod` 폴더를 상위에 만들고, `prod/services/webserver-cluster` 하위에 `main.tf` 파일을 생성하고,  
  아래와 같이 작성해보자.

```tf
provider "aws" {
  region = "us-east-2"
}

module "webserver_cluster" {
  source = "../../../modules/services/webserver-cluster"
}
```

- 이제 `prod/services/webserver-cluster` 폴더에서 `terraform init`을 수행하면 module 초기화 단계가 추가된 것을 확인할 수 있다.

- 여기서 `terraform apply`를 수행하기 전, 한 가지 문제점이 있다. 바로 `webserver-cluster` module의 모든 값들이  
  하드코딩되어 있다는 것이다. 예를 들어 security group, ALB name 등 말이다. 그렇기에 이 모듈을 동일한 AWS 계정 내에서  
  두 번 이상 사용하면 충돌이 발생할 것이다.

- 위 문제를 해결하기 위해 `webserver-cluster` module이 설정 가능한 input을 받을 수 있도록 수정해보자.

---

## Module Inputs

- Terraform module은 input parameter를 가질 수 있다. 이를 정의하려면 _input variable_ 을 선언하는 것과 동일하게 하면 된다.  
  `modules/services/webserver-cluster/variables.tf` 파일을 아래처럼 작성해보자.

```tf
variable "cluster_name" {
  description = "The name to use for all the cluster resources"
  type        = string
}

variable "db_remote_state_bucket" {
  description = "The name of the S3 bucket for the database's remote state"
  type        = string
}

variable "db_remote_state_key" {
  description = "The path for the database's remote state in S3"
  type        = string
}
```

- 다음으로 기존에 있던 `modules/services/webserver-cluster/main.tf`가 하드코딩된 값이 아닌 variable을 사용하도록 수정해보자.  
  아래는 `aws_lb` 리소스만 수정했지만, 이름이 필요한 모든 리소스에 동일하게 적용해주자.

```tf
#..

resource "aws_lb" "example" {
  name               = "${var.cluster_name}-alb"
  load_balancer_type = "application"
  subnets            = data.aws_subnets.default.ids
  security_groups    = [aws_security_group.alb.id]
}

#..

data "terraform_remote_state" "db" {
  backend = "s3"
  config = {
    bucket = var.db_remote_state_bucket
    key    = var.db_remote_state_key
    region = "us-east-2"
  }
}
```

- 이제 `stage/services/webserver_cluster/main.tf`와 `prod/services/webserver_cluster/main.tf`에서  
  모듈 선언부 내에 input variable들을 각각 지정해주자.

```tf
# stage/services/webserver_cluster/main.tf
provider "aws" {
  region = "us-east-2"
}

module "webserver_cluster" {
  source = "../../../modules/services/webserver-cluster"

  cluster_name           = "webservers-stage"
  db_remote_state_bucket = "terraform-up-and-running-state-roy-ra"
  db_remote_state_key    = "stage/data-stores/mysql/terraform.tfstate"
}

# prod/services/webserver_cluster/main.tf

provider "aws" {
  region = "us-east-2"
}

module "webserver_cluster" {
  source = "../../../modules/services/webserver-cluster"

  cluster_name           = "webservers-prod"
  db_remote_state_bucket = "terraform-up-and-running-state-roy-ra"
  db_remote_state_key    = "prod/data-stores/mysql/terraform.tfstate"
}
```

- 위에서 볼 수 있듯이 module에 input variable을 지정하는 것은 resource에 인자를 지정하는 것과 동일한 문법을 사용한다.

- 여기까지 해서 이름과 database remote state을 위한 input variable을 지정해보았는데, module의 다른 파라미터들도  
  직접 설정할 수 있게끔 해보자. 예를 들어, staging 환경에는 prod보다 더 적은 수의 서버를 띄우고 싶을 수 있다.  
  이를 위해 3개의 input variable을 더 추가해보자.

```tf
# modules/services/webserver-cluster/variables.tf

#..
variable "instance_type" {
  description = "The type of EC2 instances to run(e.g. t2.micro)"
  type        = string
}

variable "min_size" {
  description = "The minimum number of EC2 instances in the ASG"
  type        = number
}

variable "max_size" {
  description = "The maximum number of EC2 instances in the ASG"
  type        = number
}
```

- 다음으로 `modules/services/webserver-cluster/main.tf`의 launch configuration과 ASG 리소스를 수정해보자.

```
resource "aws_launch_configuration" "example" {
  image_id        = "ami-0fb653ca2d3203ac1"
  instance_type   = var.instance_type
  security_groups = [aws_security_group.instance.id]

  user_data = templatefile("user-data.sh", {
    server_port = var.server_port
    db_address  = data.terraform_remote_state.db.outputs.address
    db_port     = data.terraform_remote_state.db.outputs.port
  })

  lifecycle {
    create_before_destroy = true
  }
}

resource "aws_autoscaling_group" "example" {
  launch_configuration = aws_launch_configuration.example.name

  min_size = var.min_size
  max_size = var.max_size

  vpc_zone_identifier = data.aws_subnets.default.ids

  target_group_arns = [aws_lb_target_group.asg.arn]
  health_check_type = "ELB"

  tag {
    key                 = "Name"
    value               = var.cluster_name
    propagate_at_launch = true
  }
}
```

- 마지막으로 stage, prod 하위의 module 정의부 각각에 input variable을 지정해주자.

```tf
# stage/services/webserver_cluster/main.tf
provider "aws" {
  region = "us-east-2"
}

module "webserver_cluster" {
  source = "../../../modules/services/webserver-cluster"

  cluster_name           = "webservers-stage"
  db_remote_state_bucket = "terraform-up-and-running-state-roy-ra"
  db_remote_state_key    = "stage/data-stores/mysql/terraform.tfstate"

  instance_type = "t2.micro"
  min_size      = 2
  max_size      = 2
}


# prod/services/webserver_cluster/main.tf
provider "aws" {
  region = "us-east-2"
}

module "webserver_cluster" {
  source = "../../../modules/services/webserver-cluster"

  cluster_name           = "webservers-prod"
  db_remote_state_bucket = "terraform-up-and-running-state-roy-ra"
  db_remote_state_key    = "prod/data-stores/mysql/terraform.tfstate"

  instance_type = "t3.medium"
  min_size      = 2
  max_size      = 10
}

```

---

## Module Locals

- 모듈의 input을 정의하기 위해 input variable을 활용해도 좋지만, 만약 모듈 내에 변수를 지정해 특정 연산을 수행하거나  
  변수를 설정 가능한 input으로 표출시키지 않으면서 코드를 DRY하게 유지하고 싶으면 어떻게 해야할까?

- 예를 들어, `webserver-cluster` module의 security group, ALB, ALB listener에는 동일한 포트 번호가 반복되어 사용되고 있다.  
  그리고 security group만 보더라도 `0.0.0.0/0`, `0`, `-1` 등이 반복되어 사용되고 있다.

- 위와 같은 _magical value_ 들이 코드 곳곳에 사용되게 되면 유지 보수성과 가독성이 모두 떨어지게 된다.  
  물론 이들을 input variable로 빼내 사용하도록 할 수 있지만, 그렇다면 해당 모듈을 사용하는 사람이 실수로라도 해당 값들을  
  덮어쓰거나 변경할 수 있기 때문에 다양한 문제가 발생할 수 있다.

- 이런 상황에 사용하는 것이 _local value_ 이다.

```tf
locals {
  http_port    = 80
  any_port     = 0
  any_protocol = -1
  all_ips      = ["0.0.0.0/0"]
}
```

- local value는 `locals` block에 지정할 수 있으며, 사용할 때는 `local.<NAME>` 형식을 사용하면 된다.  
  예를 들어, local value를 활용하는 security group의 정의는 아래와 같다.

```tf
resource "aws_security_group" "alb" {
  name = "${var.cluster_name}-sg"

  ingress = [{
    cidr_blocks      = local.all_ips
    from_port        = local.http_port
    protocol         = "tcp"
    to_port          = local.http_port
    security_groups  = []
    self             = false
    description      = "Example SG"
    prefix_list_ids  = []
    ipv6_cidr_blocks = []
  }]
  egress = [{
    cidr_blocks      = local.all_ips
    from_port        = local.any_port
    protocol         = "all"
    to_port          = local.any_port
    security_groups  = []
    self             = false
    description      = "Example SG"
    prefix_list_ids  = []
    ipv6_cidr_blocks = []
  }]
}
```

- 이렇게 local value를 활용하면 코드의 가독성이 높아지고, 유지 보수하기 쉬워진다.

---

## Module Outputs

- 아래 Terraform 코드는 ASG의 scheduled scaling을 활용한 autoscaling rule이다.  
  이 기능을 production 환경에만 사용하고 싶어서 모듈에 넣는 대신, `prod/services/webserver-cluster/main.tf`에 직접 정의했다고 해보자.

```tf
resource "aws_autoscaling_schedule" "scale_out_during_business_hours" {
  scheduled_action_name = "scale-out-during-business-hours"
  min_size              = 2
  max_size              = 10
  desired_capacity      = 10
  # scale out to 10 instances every day at 9am
  recurrence = "0 9 * * *"
}

resource "aws_autoscaling_schedule" "scale_in_at_night" {
  scheduled_action_name = "scale-in-at-night"
  min_size              = 2
  max_size              = 10
  desired_capacity      = 2
  # scale in to 2 instances every day at 5pm
  recurrence = "0 17 * * *"
}
```

- 위의 `aws_autoscaling_schedule` 설정에는 한 가지 필수 속성이 누락되어 있는데, 바로 `autoscaling_group_name`으로  
  ASG의 이름을 지정해줘야 한다. 하지만 ASG의 정의는 `webserver-cluster` module에 있다. 이 경우 어떻게 해야할까?

- Terraform module 또한 _output variable_ 을 지원한다. 예를 들어, 아래와 같이 모듈 코드에 output을 지정해줄 수 있다.

```tf
output "asg_name" {
  value       = aws_autoscaling_group.example.name
  description = "The name of the Auto Scaling Group"
}
```

- Module output variable에는 `module.<MODULE_NAME>.<OUTPUT_NAME>`의 형식으로 접근할 수 있다.  
  기존에 작성했던 scale out, scale in 설정 코드에 아래 부분을 추가해주자.

```tf
resource "aws_autoscaling_schedule" "scale_out_during_business_hours" {
  scheduled_action_name = "scale-out-during-business-hours"

  # ..

  autoscaling_group_name = module.webserver_cluster.asg_name
}

resource "aws_autoscaling_schedule" "scale_in_at_night" {
  scheduled_action_name = "scale-in-at-night"

  # ..

  autoscaling_group_name = module.webserver_cluster.asg_name
}
```

---

## Module Gotchas

- 모듈을 생성할 때 다음 두 가지 사항에 유의해야 한다.

  - File paths
  - Inline blocks

### File Paths

- 이전에 user data script를 외부 파일로 빼내고 전달하기 위해 `templatefile()` 이라는 built-in function을 사용했었다.  
  이 함수를 사용할 때 유의해야할 점은 `templatefile()`에 전달하는 경로는 무조건 상대 경로여야 한다는 점이다.  
  그렇다면 어디를 기준으로 상대적인걸까?

- 기본적으로 Terraform은 현재 작업 디렉토리를 기준으로 상대 경로를 계산한다. 즉, `templatefunction()`의 위치가  
  `terraform apply`를 하는 위치와 동일해야 한다는 것이다.

- 이 문제를 해결하기 위해 _path reference_ 라는 표현식을 사용할 수 있는데, 이는 `path.<TYPE>` 의 형식을 따른다.  
  Terraform은 아래의 path reference 방식들을 제공한다.

  - `path.module`: 표현식이 정의된 모듈의 filesystem path 반환
  - `path.root`: Root module의 filesystem path 반환
  - `path.cwd`: 현재 작업 디렉토리의 filesystem path 반환.

- 예를 들어 위에서 말한 user data script의 경우, script는 `module/services/webserver-cluster/user-data.sh`에 위치해  
  있기 때문에 `path.module`을 사용해야 한다.

```tf
# module/services/webserver-cluster/main.tf
resource "aws_launch_configuration" "example" {
  #..
  user_data = templatefile("${path.module}/user-data.sh", {
    server_port = var.server_port
    db_address  = data.terraform_remote_state.db.outputs.address
    db_port     = data.terraform_remote_state.db.outputs.port
  })
}
```

### Inline blocks

- Terraform 리소스들은 inline block 또는 별도 리소스로 정의할 수 있다.  
  그 중 _inline block_ 은 아래 형식으로 정의된 리소스를 말한다.

  ```tf
  resource "xxx" "yyy" {
    <NAME> {
      [CONFIG...]
    }
  }
  ```

  - `<NAME>`: inline block의 이름(e.g. `ingress`)
  - `CONFIG`: 해당 inline block을 위한 하나 이상의 인자

- 만약 inline block과 별도 리소스를 섞어 사용하면 Terraform의 설계상 문제로 설정이 충돌하기에 하나가 덮어씌워진다는 에러가 발생한다.  
  따라서 모듈을 생성할 때는, 최대한 별도 리소스로 정의하는 것이 좋다.

- 별도 리소스로 정의하는 것은 어디서든지 사용될 수 있다는 장점이 있다. 반면 inline block으로 생성된 리소스들은 해당 리소스를  
  생성하는 모듈 내에서만 사용할 수 있다.

- 예를 들어, 아래는 inline block으로 정의된 리소스이다.

```tf
resource "aws_security_group" "alb" {
  name = "${var.cluster_name}-sg"

  ingress = [{
    cidr_blocks      = local.all_ips
    from_port        = local.http_port
    protocol         = "tcp"
    to_port          = local.http_port
    security_groups  = []
    self             = false
    description      = "Example SG"
    prefix_list_ids  = []
    ipv6_cidr_blocks = []
  }]
  egress = [{
    cidr_blocks      = local.all_ips
    from_port        = local.any_port
    protocol         = "all"
    to_port          = local.any_port
    security_groups  = []
    self             = false
    description      = "Example SG"
    prefix_list_ids  = []
    ipv6_cidr_blocks = []
  }]
}
```

- 이러한 방식으로 정의하면 모듈 밖에서 추가적인 ingress, egress 규칙을 추가할 방법이 없다.  
  모듈을 더욱 유연하게 만들기 위해서는 아래처럼 별도 리소스로 ingress, egress 규칙을 빼내어 정의해야 한다.

```tf
resource "aws_security_group" "alb" {
  name = "${var.cluster_name}-sg"
}

resource "aws_security_group_rule" "allow_http_inbound" {
  type              = "ingress"
  security_group_id = aws_security_group.alb.id
  from_port         = local.http_port
  to_port           = local.http_port
  protocol          = local.tcp_protocol
  cidr_blocks       = local.all_ips
  ipv6_cidr_blocks  = []
  prefix_list_ids   = []
  self              = false
}

resource "aws_security_group_rule" "allow_all_outbound" {
  type              = "egress"
  security_group_id = aws_security_group.alb.id
  from_port         = local.any_port
  to_port           = local.any_port
  protocol          = "all"
  cidr_blocks       = local.all_ips
  ipv6_cidr_blocks  = []
  prefix_list_ids   = []
  self              = false
}
```

- 이제 모듈 밖에서 추가적인 security group rule을 추가하는 방법을 살펴보자.  
  `aws_security_group_rule`에 해당 rule이 추가될 security group을 지정하기 위해 모듈에 ouptut variable을 추가하자.

```tf
# module/services/webserver-cluster/output.tf

# ..

output "alb_security_group_id" {
  value       = aws_security_group.alb.id
  description = "The ID of the security group for the load balancer"
}
```

- 이제 `stage` 환경에서 추가적인 security group rule을 추가하고 싶다면, 아래처럼 지정하면 된다.

```tf
# stage/services/webserver-cluster/main.tf

module "webserver_cluster" {
  source = "../../../modules/services/webserver-cluster"

  # ..
}

resource "aws_security_group_rule" "allow_testing_inbound" {
  type              = "ingress"
  security_group_id = module.webserver_cluster.alb_security_group_id

  from_port   = 12345
  to_port     = 12345
  protocol    = "tcp"
  cidr_blocks = ["0.0.0.0/0"]
}
```

---

## Module Versioning

- 만약 staging과 production 환경 모두가 동일한 모듈 폴더를 가리키고 있다면, 모듈 내용을 변경하는 순간 두 환경 모두 변경 사항이  
  생길 것이다. 이러한 결합은 특정 환경에 대해서만 새로운 변경 사항을 적용하는 것을 어렵게 한다.

- 이를 위해서는 _versioned module_ 을 활용할 수 있는데, 예를 들어 staging에는 v0.0.2 버전의 모듈을, production에는  
  v0.0.1 버전의 모듈을 사용하도록 지정할 수 있다.

- 지금까지 본 모듈 관련 예시들은 모듈 선언부에 `source` 파라미터를 활용해 local filepath를 가리키도록 했다.  
  File path에 더해 Terraform은 Git URL, Mercurial URL, 그리고 HTTP URL을 지원한다.

- Versioned module을 만드는 가장 쉬운 방법은 모듀을 별도의 Git repository에 저장하고, `source` 파라미터에 해당 레포지토리의  
  URL을 지정하는 것이다. 즉 아래처럼 폴더 구조가 변경된다.

  ![picture 4](/images/TFRU_19.png)

  - `modules`: 모듈 폴더
  - `live`: 각 인프라 환경을 위한 폴더

- 모듈을 위한 폴더에 가서 Github repository와의 연동을 마치고, release를 만들어보자.

  ```sh
  cd modules
  git tag -a "v0.0.1" -m "First release of webserver-cluster module"
  git push --follow-tags
  ```

- 이제 이 versioned module을 staging, production 환경 모두에 각각 `source` 파라미터에 Git URL을 지정함으로써  
  사용할 수 있다. 만약 module이 담긴 레포지토리가 `github.com/sang-w0o/ws-cluster-module`이라면, 아래처럼 변경된다.

```tf
# stage/services/webserver-cluster/main.tf

module "webserver_cluster" {
  source = "github.com/sang-w0o/ws-cluster-module//modules/services/webserver-cluster?ref=v0.0.1"

  # ..
}
```

- `source`에 지정한 URL에서 `//`는 고유 형식이다. 즉, `{GITHUB_REPO_URL}//{MODULE_PATH}` 형식으로 지정해야 한다.

---
