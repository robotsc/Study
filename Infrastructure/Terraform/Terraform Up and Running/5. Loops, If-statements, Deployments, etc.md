# Loops, If-statements, Deployments, etc.

- Terraform은 선언적 언어이다. 1장에서 다뤘던 것처럼 선언적으로 구성된 IaC는 절차적 방식보다 실제로 배포된 인프라를  
  더욱 쉽게 파악할 수 있게 해준다. 하지만 특정 작업들은 절차적 방식보다 복잡하게 해야 한다.

- 예를 들어, 보통의 선언적 언어들은 for-loop을 지원하지 않기 때문에 코드 조각을 반복하는 경우가 생길 때 적절한 방법을  
  찾아야 한다. If-statement도 마찬가지이며, 다운타임이 없는 배포 등을 선언적 언어로 표현하기란 까다롭다.

- 다행이도 Terraform은 일부 primitive들을 지원하는데, `count`라는 meta-parameter, `for_each`와 `for`문,  
  ternary operator, `create_before_destroy`라는 lifecycle block, 그리고 수많은 함수들이 있다.

## Loops

- Terraform은 looping을 위해 다양한 방법을 지원하는데, 각각은 적절한 쓰임새가 있다.

  - `count` parameter: 리소스와 모듈을 순회하기 위함
  - `for_each` expression: 리소스, 리소스 내의 inline block, 그리고 모듈을 순회하기 위함
  - `for` expression: list, map을 순회하기 위함
  - `for` string directive: 문자열 내에 존재하는 list, map을 순회하기 위함

### Loops with the count parameter

- Terraform을 처음 구성할 때는 Terraform이 리소스를 만들어내기 위한 IAM user를 콘솔을 통해 발급받을 수 있다.  
  그리고 그 후 부터는 해당 IAM user를 통해 추가적인 IAM user들을 Terraform으로 생성하고, 관리할 수 있다.

- 아래의 Terraform 코드를 보자.

```tf
# live/global/iam/main.tf

provider "aws" {
  region = "us-east-2"
}

resource "aws_iam_user" "example" {
  name = "neo"
}
```

- 위 코드는 `aws_iam_user` 리소스를 사용해 하나의 IAM user를 생성해낸다.  
  만약 3명의 IAM user를 생성해야 한다면 어떻게 해야할까?  
  GPL을 사용하면 for-loop를 활용할 수 있을 것이다.

- 하지만 Terraform은 for-loop을 지원하지 않는다. 대신, 모든 Terraform 리소스는 `count`라는 meta-parameter를  
  가질 수 있다. `count`는 Terraform의 가장 오래되고, 단순하고, 가장 한정적인 순회 메커니즘을 제공하는데,  
  단지 해당 리소스의 복사본을 몇 개 만들지를 지정하는 것에 불과하다.  
  이를 사용해 3명의 IAM user를 생성하는 코드는 아래와 같다.

```tf
resource "aws_iam_user" "example" {
  count = 3
  name  = "neo"
}
```

- 위 코드의 한 가지 문제점은 세 명의 IAM user들이 모두 동일한 이름을 가지게 된다는 것이고, 이는 IAM username이 unique해야  
  한다는 AWS의 규칙을 위반하게 된다. 이를 해결하기 위해 아래처럼 `count.index`를 사용해 loop 내의 각 순회 단계를 가져올 수 있다.

```tf
resource "aws_iam_user" "example" {
  count = 3
  name  = "neo-${count.index}"
}
```

- 위 코드를 apply하면 `neo-0`, `neo-1`, `neo-2`라는 세 개의 IAM user가 생성된다.

- 물론 `neo-0`과 같은 사용자 이름은 의미가 없고, 실용성도 떨어진다.  
  만약 `count.index`를 Terraform이 제공하는 built-in function들과 결합하면 loop 내의 각 순회를 더욱 유용하게 사용할 수 있다.

```tf
variable "user_names" {
  description = "Create IAM users with these names"
  type        = list(string)
  default     = ["neo", "trinity", "morpheus"]
}
```

- 위 변수를 선언하고, loop가 돌면서 list 내의 각 순회 번호에 맞는 원소를 가져오도록 할 수 있다.  
  아래의 내용들을 사용해야 한다.

  - _Array lookup syntax_ : Terraform에서 배열 내의 원소들을 찾아내는 방법은 일반적인 프로그래밍 언어 문법과 비슷하다.

    - `ARRAY[<INDEX>]`

    - 예를 들어, `var.user_names`의 1번 index 원소를 찾는 코드는 `var.user_names[1]`이다.

  - `length()` _function_ : Terraform은 아래의 형식을 따르는 `length()`라는 built-in function이 있다.

    - `length(<ARRAY>)`

    - 알 수 있듯이 `length()` 함수는 주어진 `ARRAY`의 원소 개수를 반환한다. 문자열 및 map에도 사용할 수 있다.

- 위의 두 가지를 활용해 작성된 코드는 아래와 같다.

```tf
resource "aws_iam_user" "example" {
  count = length(var.user_names)
  name  = var.user_names[count.index]
}
```

- 이제 neo, trinity, 그리고 morpheus라는 이름을 가진 3명의 IAM user가 생성된다.

- 한 가지 유의할 점은 리소스 내부에 `count`를 사용하면, 해당 리소스는 단일 리소스가 아닌 리소스들의 배열이 된다.  
  따라서 만약 참조하려면 아래와 같이 사용해야 한다.

```tf
output "first_arn" {
  value       = aws_iam_user.example[0].arn
  description = "The ARN for the first user"
}
```

- 만약 모든 IAM user들의 ARN을 원한다면, index를 지정하는 대신 `*`(splat expression)을 사용할 수 있다.

```tf
output "all_arns" {
  value       = aws_iam_user.example[*].arn
  description = "The ARNs for all users"
}
```

- Terraform 0.13부터는 module에도 `count`를 사용할 수 있다.  
  예를 들어, `modules/landing-zone/iam-user/main.tf`에 아래와 같이 하나의 IAM user를 생성하는 코드가 있다고 하자.

```tf

provider "aws" {
  region = "us-east-2"
}

resource "aws_iam_user" "example" {
  name = var.user_name
}
```

- 이 모듈을 사용하는 쪽에서 username을 input variable로 전달한다.

```tf
variable "user_name" {
  description = "The username to use"
  type        = string
}
```

- 그리고 module은 생성된 IAM user의 ARN을 output variable로 내보낸다.

```tf
output "user_arn" {
  value       = aws_iam_user.example.arn
  description = "The ARN of the created IAM user"
}
```

- 이제 위 코드를 module로 불러와 아래처럼 3명의 IAM user들을 생성하도록 할 수 있다.

```tf
provider "aws" {
  region = "us-east-2"
}

variable "user_names" {
  description = "Create IAM users with these names"
  type        = list(string)
  default     = ["neo", "trinity", "morpheus"]
}

module "users" {
  source = "../../../../modules/landing-zone/iam-user"

  count     = length(var.user_names)
  user_name = var.user_names[count.index]
}

output "all_arns" {
  value       = aws_iam_user.example[*].arn
  description = "The ARNs for all users"
}
```

- 하지만 `count`는 사용성을 급격히 떨어뜨리는 두 가지의 한계점이 존재한다.  
  첫째로, 전체 리소스를 순쇠하기 위해 `count`를 사용할 수 있음에도 리소스 내의 inline block을 순회하기 위해서는 사용할 수 없다.

- 예를 들어, 아래의 `aws_autoscaling_group` 리소스에 tag가 어떻게 지정되는지 보자.

```tf
resource "aws_autoscaling_group" "example" {
  launch_configuration = aws_launch_configuration.example.name
  vpc_zone_identifier  = data.aws_subnets.default.ids
  target_group_arns    = [aws_lb_target_group.asg.arn]
  health_check_type    = "ELB"

  min_size = var.min_size
  max_size = var.max_size

  tag {
    key                 = "Name"
    value               = var.cluster_name
    propagate_at_launch = true
  }
}
```

- `tag`는 `key`, `value`, `propagate_at_launch`를 위한 값들을 가진 inline block을 사용해야 한다.  
  위의 코드는 하나의 태그만을 하드코딩하지만, 사용자들이 custom tag를 넣을 수 있게 하고 싶다면 어떻게 해야할까?  
  `count` parameter를 사용해 tag를 순회하고 동적으로 inline `tag` block을 만들어내고 싶다는 생각을 할 것이다.  
  하지만 inline block에 대해서는 `count`를 사용할 수 없다.

- `count`의 두 번째 한계점으로는 값을 바꾸려고 할 때 등장한다.  
  이전에 생성한 IAM user들을 보자.

```tf
variable "user_names" {
  description = "Create IAM users with these names"
  type        = list(string)
  default     = ["neo", "trinity", "morpheus"]
}
```

- 여기서 `trinity`를 제거하고 `plan`을 수행하면 어떻게 될까?  
  우리가 기대하는 것은 trinity만 삭제되는 것이지만, 실제 결과는 trinity의 이름을 morpheus로 바꾸고, morpheus를 제거하는 것이다.  
  이렇게 되는 이유는 리소스에 대해 `count`를 사용하게 되면 해당 리소스가 리소스들의 배열이 되어버리기 때문이다.  
  불행히도 Terraform은 배열 내의 리소스를 index를 사용해 식별한다. 즉, 3명의 IAM user를 생성했을 때 Terraform은 내부적으로  
  아래 정보를 저장한다.

  ```
  aws_iam_user.example[0] = neo
  aws_iam_user.example[1] = trinity
  aws_iam_user.example[2] = morpheus
  ```

- 그리고 trinity를 제거하는 순간, 아래처럼 바뀐다.

  ```
  aws_iam_user.example[0] = neo
  aws_iam_user.example[1] = morpheus
  ```

- morpheus가 2번에서 1번으로 index가 바뀌었기 때문에, Terraform은 trinity가 morpheus로 바뀌었다고 인식하게 된다.  
  그래서 위와 같은 `plan` 결과가 나왔던 것이다.

- 위의 두 가지 한계점을 극복하기 위해서, Terraform 0.12에 `for_each` 표현식이 등장했다.

---

### Loops with `for_each` expressions

- `for_each` 표현식은 list, set, map을 순회해 아래 내용을 수행할 수 있다.

  - 전체 리소스의 복사본 생성
  - 리소스 내의 inline block의 복사본 생성
  - 모듈의 복사본 생성

- 처음으로 `for_each`를 사용해 전체 리소스의 복사본을 생성하는 방법을 보자. 문법은 아래와 같다.

```tf
resource "<PROVIDER>_<TYPE>" "<NAME>" {
  for_each = <COLLECTION>

  [CONFIG...]
}
```

- `COLLECTION`: 순회할 set 혹은 map
- `CONFIG`: 해당 리소스가 사용하는 하나 이상의 파라미터

- `CONFIG` 내에서는 `each.key`와 `each.value`를 사용해 `COLLECTION`을 순회할 때의 현재 원소의 key, value를 사용할 수 있다.

- 아래는 `for_each`를 리소스에 적용해 3명의 IAM user를 만드는 코드이다.

```tf
resource "aws_iam_user" "example" {
  for_each = toset(var.user_names)
  name     = each.value
}
```

- 위에서 `toset()`은 `var.user_names` list를 set으로 변경해주기 위해 사용했는데, 이는 `for_each`가 set 혹은 map에 대해서만  
  사용 가능하기 때문이다. Map을 순회할 때 `each.key`는 원소의 key, `each.value`는 원소의 value를 나타내고, set을 순회할 때는  
  `each.key`와 `each.value`가 동일하게 원소의 value를 나타낸다.

- `for_each`를 리소스에 사용하는 순간, 해당 리소스는 리소스들로 이뤄진 map이 된다.  
  이를 확인하기 위해 기존에 있던 `all_users` output variable을 아래처럼 수정해보자.

```tf
output "all_users" {
  value = aws_iam_user.example
}
```

- `terraform apply`를 수행하면 output에 3명의 IAM user가 나타난다.  
  key는 각 IAM user의 name이고, value는 해당 IAM user의 정보이다.

- 이전에 있던 `all_arns` output variable은 아래와 같이 `values()` built-in function과 splat expression을  
  함께 사용해 구할 수 있다.

```tf
output "all_arns" {
  value = values(aws_iam_user.example)[*].arn
}
```

- 이렇게 `count`를 사용할 때 리소스가 배열이 되었던 것과 달리, `for_each`를 사용할 때 리소스가 map이 되는 것은 큰 이점을  
  가져다준다. Collection의 중간에 있는 원소(리소스)를 안전하게 제거할 수 있기 때문이다. 이전과 마찬가지로 trinity를 제거하고  
  `plan`을 수행하면, 우리가 원하는대로 trinity user만 제거될 것임을 확인할 수 있다.

- `for_each`는 모듈에 대해서도 사용할 수 있다. 이전에 만들었던 `iam-user` module에 대해서 아래처럼 `for_each`를  
  사용해 3명의 IAM user를 만들 수 있다.

```tf
module "users" {
  source = "../../../../modules/landing-zone/iam-user"

  for_each  = toset(var.user_names)
  user_name = each.value
}

output "all_arns" {
  value       = values(module.users)[*].user_arn
  description = "The ARNs for all users"
}
```

- 이제 `for_each`의 또다른 장점 중 하나인 리소스 내에 여러 개의 inline block을 만들 수 있는 장점에 대해 살펴보자.  
  예를 들어, `for_each`를 사용해 `webserver-cluster` module내의 ASG에 `tag` inline block들을 동적으로  
  만들어낼 수 있다. 우선 아래처럼 사용자들의 custom tag를 정의하도록 하기 위해 `custom_tags`라는 새로운 map 타입의  
  input variable을 선언해보자.

```tf
# modules/services/webserver-cluster/variables.tf
variable "custom_tags" {
  description = "Custom tags to set onthe Instances in the ASG"
  type        = map(string)
  default     = {}
}
```

- 그리고 아래처럼 module을 사용하는 쪽에 `custom_tags`를 정의해보자.

```tf
module "webserver_cluster" {
  source = "../../../modules/services/webserver-cluster"

  cluster_name           = "webservers-prod"
  db_remote_state_bucket = "terraform-up-and-running-state-roy-ra"
  db_remote_state_key    = "prod/data-stores/mysql/terraform.tfstate"

  instance_type = "t3.medium"
  min_size      = 2
  max_size      = 10

  custom_tags = {
    Owner     = "team-foo"
    ManagedBy = "terraform"
  }
}
```

- 위 코드는 2개의 tag를 지정하는데, 하나는 해당 ASG의 소유자가 누구인지이고, 다른 하나는 리소스 관리 주체를 표현한다.

- 이제 custom tag들을 받아왔는데, 실제로 이를 적용하는 방법은 어떻게 될까?  
  여기서 필요한 것은 `var.custom_tags`를 순회할 수 있는 for-loop이다. 이를 위해 `for_each` 표현식을 사용할 수 있다.  
  `for_each` 표현식을 사용해 동적으로 inline block을 만들어내는 문법은 아래와 같다.

  ```tf
  dynamic "<VAR_NAME>" {
    for_each = <COLLECTION>

    content {
      [CONFIG...]
    }
  }
  ```

  - `VAR_NAME`: 순회할 때마다의 값을 담는 변수 이름
  - `COLLECTION`: 순회할 list 또는 map
  - `content`: 각 순회에서 만들어낼 inline block, `content` 내에서는 `<VAR_NAME>.key`와 `<VAR_NAME>.value`를 사용해  
    `COLLECTION`을 순회할 때의 현재 원소의 key, value를 사용할 수 있다. 한 가지 유의할 점은 list에 대해 `for_each`를  
    사용할 때 `key`는 index 번호, `value`는 실제 원소의 값이다.

- 이를 모두 종합해 아래처럼 `for_each`를 사용해 `tag` inline block을 동적으로 만들어낼 수 있다.

```tf
resource "aws_autoscaling_group" "example" {
  launch_configuration = aws_launch_configuration.example.name
  vpc_zone_identifier  = data.aws_subnets.default.ids
  target_group_arns    = [aws_lb_target_group.asg.arn]
  health_check_type    = "ELB"

  min_size = var.min_size
  max_size = var.max_size

  tag {
    key                 = "Name"
    value               = var.cluster_name
    propagate_at_launch = true
  }

  dynamic "tag" {
    for_each = var.custom_tags

    content {
      key                 = tag.key
      value               = tag.value
      propagate_at_launch = true
    }
  }
}
```

#### Enforcing Tagging Standards

- 하나의 Terraform module이 정의하는 모든 리소스가 같은 tag를 가지도록 강제하는 방법 중 하나로 `default_tags`를 사용할 수 있다.

```tf
provider "aws" {
  region = "us-east-2"

  default_tags {
    tags = {
      Owner     = "team-foo"
      ManagedBy = "terraform"
    }
  }
}
```

---

### Loops with `for` expressions

- 지금까지 loop를 활용해 리소스를 여러 개 만들고, inline block을 동적으로 만들어내는 방법을 보았다.  
  그렇다면 단 하나의 variable 혹은 parameter를 순회하고자 할 땐 어떻게 해야 할까?

- 아래와 같은 list 변수가 있다고 해보자.

```tf
variable "names" {
  description = "Create IAM users with these names"
  type        = list(string)
  default     = ["neo", "trinity", "morpheus"]
}
```

- name의 원소들을 모두 대문자로 변환하고 싶다면 어떻게 해야할까? Python같은 GPL은 아래처러 for loop를 사용할 수 있다.

```py
names = ["neo", "trinity", "morpheus"]

upper_case_names = []
for name in names:
    upper_case_names.append(name.upper())

print(upper_case_names)

upper_case_names_2 = [name.upper() for name in names]
print(upper_case_names_2)

upper_case_name3 = [name.upper() for name in names if len(name) < 5]
print(upper_case_name3)
```

- Terraform은 위와 유사한 기능을 _for_ 표현식으로 제공한다. 기본 문법은 아래와 같다.

```tf
[for <ITEM> in <LIST> : <OUTPUT>]
```

- `LIST`: 순회할 list
- `ITEM`: `LIST`내의 각 원소를 할당할 지역 변수명
- `OUTPUT`: `ITEM`을 특정 방식으로 변환하는 표현식

- 예를 들어, list 내 각 원소들을 대문자로 변환하는 코드는 아래와 같다.

```tf
output "upper_names" {
  value = [for name in var.names : upper(name)]
}

output "upper_names_2" {
  value = [for name in var.names : upper(name) if length(name) < 5]
}
```

- `for` 표현식으로 map을 순회하는 기본 문법은 아래와 같다.

```tf
[for <KEY>, <VALUE> in <MAP> : <OUTPUT>]
```

- 아래의 `hero_thousand_faces` 라는 map 변수가 있다고 해보자.

```tf
variable "hero_thousand_faces" {
  description = "map"
  type        = map(string)
  default = {
    neo      = "hero"
    trinity  = "love interest"
    morpheus = "mentor"
  }
}

output "bios" {
  value = [for name, role in var.hero_thousand_faces : "${name} is a ${role}"]
}
```

- 위 코드에 대해 `apply`를 수행하면, 아래의 출력 결과가 나온다.

```
bios = [
  "morpehus is a mentor",
  "neo is a hero",
  "trinity is a love interest",
]
```

- 아래처럼 `for` 표현식으로 list 대신 map을 반환하도록 할 수 있다.

```tf
# Loop over a list and output a map
{for <ITEM> in <LIST>: <OUTPUT_KEY> => <OUTPUT_VALUE>}

# Loop over a map and output a map
{for <KEY>, <VALUE> in <MAP>: <OUTPUT_KEY> => <OUTPUT_VALUE>}
```

- 유일한 차이점은 표현식을 `[]` 대신 `{}`로 감싼다는 것이고, 각 순회마다 하나의 값을 반환하지 않고 key, value를 함께  
  반환한다는 것이다. 아래는 map을 순회해 key, value를 모두 대문자로 변환하는 코드이다.

```tf
output "upper_roles" {
  value = {for name, role in var.hero_thousand_faces : upper(names) => upper(role)}
}
```

- 결과는 아래와 같다.

```tf
upper_roles = {
  "MORPHEUS" = "MENTOR"
  "NEO" = "HERO"
  "TRINITY" = "LOVE INTEREST"
}
```

### Loops with the `for` string directive

- 이 책의 앞부분에서 string interpolation, 즉 아래처럼 문자열 내에서 Terraform 코드를 참조하는 방식을 보았다.

```tf
"Hello, ${var.name}"
```

- _String directive_ 는 for-loop 와 if문 등의 기능을 문자열 내에서 string interpolation과 비슷한 문법으로 사용하도록 해준다.  
  String interpolation은 `${}`의 형식을 가졌다면, string directive는 `%{}`의 형식을 가진다.

- Terraform은 두 가지 종류의 string directive를 지원하는데, 하나는 for-loop이고 다른 하나는 조건문이다.  
  여기서는 for-loop만 보고, 조건문은 이 장의 마지막 부분에서 다시 볼 것이다.

- `for` string directive는 아래의 형식을 따른다.

```tf
%{for <ITEM> in <COLLECTION> }<BODY>%{endfor}
```

- `COLLECTION`: 순회할 list 혹은 map
- `ITEM`: `COLLECTION`을 순회할 때마다의 원소를 할당할 지역 변수명
- `BODY`: 각 순회마다 출력할 내용

```tf
variable "names" {
  description = "Create IAM users with these names"
  type        = list(string)
  default     = ["neo", "trinity", "morpheus"]
}

output "for_directive" {
  value = "%{for name in var.names}${name}, %{endfor}"
}
```

- `apply` 결과는 아래와 같다.

```
for_directive = "neo, trinity, morpheus, "
```

- `for` string directive에서는 for-loop의 index 번호를 가져와 사용할 수도 있다.

```tf
output "for_directive_index" {
  value = %{for i, name in var.names }(${i}) ${name}, %{endfor}
}
```

- `apply` 결과는 아래와 같다.

```
for_directive_index = "(0) neo, (1) trinity, (2) morpheus, "
```

- 위 두 예제 모두 마지막에 `,`가 있음을 보자. 이는 다음에 살펴볼 조건문, 특히 `if` string directive로 고칠 수 있다.

---

## Conditionals

- Terraform이 loop를 위해 다양한 방법을 지원하는 것처럼, 조건문도 여러 가지 방법으로 수행할 수 있다.

  - `count` _parameter_ : 조건적인 리소스에 사용
  - `for_each`, `for` _expressions_: 조건적인 리소스 혹은 리소스 내의 inline block에 사용
  - `if` _string directive_ : 문자열 내에서 조건문을 사용

### Conditionals with `count` parameter

#### If-statements with the `count` parameter

- 4장에서 우리는 web server cluster를 배포하기 위한 청사진으로 사용될 수 있는 Terraform module을 생성했다.  
  해당 모듈은 ASG, ALB, security groups, 그리고 다른 리소스들을 생성했다. 이 모듈이 생성하지 않은 것 한 가지는  
  scheduled action 이다. Production 환경의 클러스터만 scale을 하고 싶어서 `aws_autoscaling_schedule` 리소스를  
  직접 `live/prod/services/webserver-cluster/main.tf` 파일에 직접 생성해주었다.  
  그렇다면 이렇게 하지 않고 `aws_autoscaling_schedule` 리소스를 모듈에 정의하고, 조건적으로 해당 리소스를 생성하도록 할 수는 없을까?

- 여기서 `count` parameter를 활용해 if문을 사용하는 것과 같은 결과를 얻어낼 수 있다.

  - `count`를 1로 지정하면, 해당 리소스는 1개 생성되고, 0으로 지정하면 생성되지 않는다.
  - Terraform은 조건문 표현식을 `<CONDITION> ? <TRUE_VAL> : <FALSE_VAL>` 형식으로 지원한다.  
    이를 _ternary syntax_ 라고 하며, 다른 프로그래밍 언어들과 형식이 유사하다.

- 위의 내용을 조합해 `webserver-cluster` module을 아래처럼 수정할 수 있다.

```tf
variable "enable_autoscaling" {
  description = "If set to true, enable autoscaling"
  type        = bool
}

resource "aws_autoscaling_schedule" "scale_out_during_business_hours" {
  count                 = var.enable_scheduling ? 1 : 0
  scheduled_action_name = "scale-out-during-business-hours"
  min_size              = 2
  max_size              = 10
  desired_capacity      = 10
  # scale out to 10 instances every day at 9am
  recurrence = "0 9 * * *"

  autoscaling_group_name = aws_autoscaling_group.example.name
}

resource "aws_autoscaling_schedule" "scale_in_at_night" {
  count                 = var.enable_scheduling ? 1 : 0
  scheduled_action_name = "scale-in-at-night"
  min_size              = 2
  max_size              = 10
  desired_capacity      = 2
  # scale in to 2 instances every day at 5pm
  recurrence = "0 17 * * *"

  autoscaling_group_name = aws_autoscaling_group.example.name
}
```

#### If-else statements with the `count` parameter

- If-else statement도 `count` parameter를 사용해 구현할 수 있다.

- 이 장의 첫 부분에서 우리는 EC2에 대한 read-only access를 가진 3명의 IAM user를 만들었다.  
  여기서 neo에게만 CloudWatch에 대한 접근 권한을 부여하고 싶고, Terraform 설정을 apply하는 사람이 neo에게  
  읽기, 혹은 전체 권한을 부여할지 여부를 결정하도록 하고 싶으면 어떻게 해야 할까?

- 아래는 CloudWatch read-only access, 그리고 full access를 부여하는 IAM policy이다.

```tf
resource "aws_iam_policy" "cloudwatch_read_only" {
  name   = "cloudwatch-read-only"
  policy = data.aws_iam_policy_document.cloudwatch_read_only.json
}

data "aws_iam_policy_document" "cloudwatch_read_only" {
  statement {
    effect = "Allow"
    actions = [
      "cloudwatch:Describe*",
      "cloudwatch:Get*",
      "cloudwatch:List*"
    ]
    resources = ["*"]
  }
}

resource "aws_iam_policy" "cloudwatch_full_access" {
  name   = "cloudwatch-full-access"
  policy = data.aws_iam_policy_document.cloudwatch_full_access.json
}

data "aws_iam_policy_document" "cloudwatch_full_access" {
  statement {
    effect = "Allow"
    actions = [
      "cloudwatch:*"
    ]
    resources = ["*"]
  }
}
```

- 목표는 위의 IAM policy들 중 하나를 "neo"에게 할당하는 것이고, 이를 `give_neo_cloudwatch_full_access`라는  
  input variable로 판단하는 것이다.

```tf
variable "give_neo_cloudwatch_full_access" {
  description = "If true, neo gets full access to CloudWatch"
  type        = bool
}
```

- 이를 위해 `count` parameter를 사용한 모습은 아래와 같다.

```tf
resource "aws_iam_user_policy_attachment" "neo_cloudwatch_full_access" {
  count = var.give_neo_cloudwatch_full_access ? 1 : 0

  user       = aws_iam_user.example.name
  policy_arn = aws_iam_policy.cloudwatch_full_access.arn
}

resource "aws_iam_user_policy_attachment" "neo_cloudwatch_read_only" {
  count = var.give_neo_cloudwatch_full_access ? 0 : 1

  user       = aws_iam_user.example.name
  policy_arn = aws_iam_policy.cloudwatch_read_only.arn
}
```

- 위 코드는 두 개의 `aws_iam_user_policy_attachment` 리소르를 정의한다. 첫 번째는 CloudWatch full access를 부여하고,  
  두 번째는 CloudWatch read-only access를 부여한다.

- 이제 if-else 조건에 기반해 리소스를 생성할 수 있게 되었다. 그렇다면 실제로 만들어진 리소스에 대한 속성을 알고 싶으면 어떻게 해야할까?  
  예를 들어, 실제로 부여된 IAM policy의 ARN을 `neo_cloudwatch_policy_arn`으로 output하고 싶다면 어떻게 해야할까?

- 가장 단순한 방법은 ternary syntax를 사용하는 것이다.

```tf
output "neo_cloudwatch_policy_arn" {
  value = {
    var.give_neo_cloudwatch_full_access
    ? aws_iam_user_policy_attachment.neo_cloudwatch_full_access.policy_arn
    : aws_iam_user_policy_attachment.neo_cloudwatch_read_only.policy_arn
  }
}
```

- 이는 지금 당장은 잘 동착할 수 있어도, 코드가 조금은 복잡하다. 만약 미래에 `aws_iam_user_policy_attachment` 리소스의  
  `count` parameter 조건식을 바꾸게 된다면 `var.give_neo_cloudwatch_full_access` 뿐만 아니라 수많은 변수에  
  의존하게 될 것이다. 결과적으로 코드는 더욱 복잡해질 것이다.

- 더욱 안전한 접근법은 `concat()` 과 `one()` 함수를 사용하는 것이다. `concat()`은 두개 이상의 list를 입력으로 받아  
  이들을 하나의 list로 만들어주고, `one()` 함수는 list를 입력으로 받아 만약 list의 길이가 0이면 null을, 길이가 1이면  
  해당 원소를 반환하고, 길이가 1보다 크면 에러를 발생시킨다. 이들을 모두 사용해 아래처럼 코드를 작성할 수 있다.

```tf
output "neo_cloudwatch_policy_arn_2" {
  value = one(concat(
    aws_iam_user_policy_attachment.neo_cloudwatch_full_access[*].policy_arn,
    aws_iam_user_policy_attachment.neo_cloudwatch_read_only[*].policy_arn
  ))
}
```

- if-else 조건문의 결과에 따라 `neo_cloudwatch_full_access` 혹은 `neo_cloudwatch_read_only` 중 하나만 한 개의 원소를  
  가지게 되고, 이들을 `concat()`으로 합쳐도 원소가 1개인 list가 생긴다. 그리고 이에 대해 `one()`을 사용했기에  
  위 코드는 if-else 조건문이 어떻게 바뀌든 정상적으로 동작하게 된다.

- 이렇게 `count`와 built-in function들을 활용해 if-else 문을 흉내내는 것은 일종의 꼼수인데, 전반적으로 잘 동작한다.  
  그리고 코드에서 볼 수 있듯이 사용자들로부터 복잡성을 감출 수도 있어서 깔끔하고 단순한 API를 제공할 수 있다.

### Conditionals with `for_each` and `for` expressions

- `count` parameter를 통해 조건적인 로직을 구현한 것과 유사하게, 이를 `for_each` 표현식으로도 할 수 있다.

- `for_each` 표현식에 아무런 원소도 없는 컬렉션을 넘기면, 결과적으로 0개의 리소스, inline block, 혹은 module이 생성될 것이다.  
  반면 원소가 있는 컬렉션을 넘기면 하나 이상의 리소스, inline block, 혹은 module이 생성될 것이다.  
  그렇다면 한 가지 의문점이 남는데, 컬렉션이 빈 것인지 아닌지를 어떻게 판단할 수 있을까?

- 해답은 `for_each` 표현식과 `for` 표현식을 조합하는 것이다.  
  예를 들어, 이전에 `webserver-cluster` module은 아래처럼 tag를 동적으로 생성했다.

```tf
dynamic "tag" {
  for_each = var.custom_tags

  content {
    key                 = tag.key
    value               = tag.value
    propagate_at_launch = true
  }
}
```

- 만약 `var.custom_tags`가 비어있다면 `for_each` 표현식은 순회할 대상이 없기에 아무런 tag도 설정되지 않을 것이다.  
  즉, 이미 여기에 조건적인 로직을 구현해 놓았다는 것이다. 하지만 여기에서 더 나아가 아래처럼 `for_each`와 `for` 표현식을  
  조합해 사용할 수 있다.

```tf
dynamic "tag" {
  for_each = {
    for key, value in var.custom_tags :
    key => upper(value)
    if key != "Name"
  }

  content {
    key                 = tag.key
    value               = tag.value
    propagate_at_launch = true
  }
}
```

- 중첩되어 있는 `for` 표현식은 `var.custom_tags`를 순회하며, 각 값을 대문자로 변환하고, `for` 표현식에서  
  조건문을 사용해 `key`가 `Name`이 아닌 대상만 대문자로 변환하도록 한다.

- 대부분의 경우, 하나 이상의 리소스나 모듈을 생성할 때 `for_each`를 `count`보다 선호해야 하는 것은 맞지만,  
  조건적인 로직이 들어갈 때는 `count`를 0 혹은 1로 설정하도록 하는 것이 `for_each`에 넘길 컬렉션을 비거나 안 비게 하는 것보다 단순하다.

### Conditionals with `if` string directive

- 이제 아래의 문법을 가지는 `if` string directive를 살펴보자.

```tf
%{ if <CONDITION> }<TRUEVAL>% { endif }
```

- `CONDITION`: boolean 값을 반환하는 표현식
- `TRUEVAL`: `CONDITION`이 true일 때 사용할 표현식

- 이전에 `for` string directive를 사용해 문자열을 순회해 comma로 구분되는 이름을 반환한적이 있다.  
  그리고 문제점은 마지막 원소에 대해서도 `,`가 붙는다는 것이었다.

```tf
variable "names" {
  description = "Create IAM users with these names"
  type        = list(string)
  default     = ["neo", "trinity", "morpheus"]
}

output "for_directive" {
  value = "%{for name in var.names}${name}, %{endfor}"
}
```

- 이를 `if` string directive를 사용해 고쳐보자.

```tf
output "for_directive_index_if" {
  value = <<EOF
  %{for i, name in var.names}
    ${name} %{if i < length(var.names) - 1}, %{endif}
  %{endfor}
  EOF
}
```

- 여기서 `apply`를 수행하면 아래의 output이 출력된다.

```
for_directive_index_if = <<EOT

  neo,

  trinity,

  morpheus


EOT
```

- 마지막 원소에 붙는 `,`는 제거되었지만, 수많은 whitespace들이 생겨났다. 이렇게 되는 이유는 HEREDOC에  
  들어가는 모든 whitespace는 그대로 출력되기 때문이다. 이는 _strip markers(~)_ 를 활용해 해결할 수 있다.

```tf
output "for_directive_index_if_strip" {
  value = <<EOF
  %{~for i, name in var.names~}
    ${name} %{if i < length(var.names) - 1}, %{endif}
  %{~endfor~}
  EOF
}
```

- 결과는 아래와 같다.

```
for_directive_index_if_strip = "neo, trinity, morpheus"
```

- 마지막으로 한 가지 개선을 해보자. 마지막 원소일 경우에는 `,`가 아니라 `.`가 붙도록 해볼 것이다.  
  여기에는 아래의 `else` string directive를 활용할 수 있다.

```tf
${ if <CONDITION> }<TRUEVAL>%{ else }<FALSEVAL>%{ endif }
```

- `FALSEVAL`: `CONDITION`이 false일 때 사용할 표현식

- 적용한 모습은 아래와 같다.

```tf
output "for_directive_index_if_else_strip" {
  value = <<EOF
  %{~for i, name in var.names~}
    ${name}%{if i < length(var.names) - 1}, %{else}.%{endif}
  %{~endfor~}
  EOF
}
```

- 결과는 아래와 같다.

```
for_directive_index_if_else_strip = "neo, trinity, morpheus."
```

---

## Zero-Downtime Deployment

- 이제 `webserver-cluster` module은 깔끔하고 단순한 API를 제공하게 되었다.  
  한 가지 중요한 의문이 있는데, 해당 모듈로 생성한 클러스터를 어떻게 갱신할 것인가? 즉, 코드를 수정해 AMI를 변경했다면, 이를 클러스터 내의 모든  
  인스턴스에 어떻게 적용할 것인가? 그리고 사용자에게 다운 타임 없이 어떻게 적용할 것인가?

- 이를 위한 첫 번째 단계는 AMI를 `modules/services/webserver-cluster/variables.tf`에 input variable로 노출시키는 것이다.  
  실 세계에서 웹 서버의 실제 코드는 AMI 내에 정의되어 있을 것이니 니로써 충분할 것이다. 하지만 이 책에서 다루는 코드는 웹 서버의 코드를  
  user data script에 저장하고 있기 때문에, AMI는 단지 아무것도 없는 Ubuntu image에 불가하다. Ubuntu의 버전을 바꾸는 것은  
  그렇게 고려할 사항이 많지 않기 때문에, AMI에 더해 user data script까지 설정하는 input variable을 아래처럼 추가해보자.

```tf
variable "ami" {
  description = "The AMI to run in the cluster"
  type        = string
  default     = "ami-0fb653ca2d3203ac1"
}

variable "server_text" {
  description = "The text the web server should run"
  type        = string
  default     = "Hello, World"
}
```

- 이제 user data script를 아래처럼 수정해 `server_text` 변수를 사용하도록 해보자.

```sh
#!/bin/bash

cat > index.html <<EOF
<h1>${server_text}</h1>
<p>DB address: ${db_address}</p>
<p>DB port: ${db_port}</p>
EOF

nohup busybox httpd -f -p ${server_port} &
```

- 마지막으로 `modules/services/webserver-cluster/main.tf`에서 launch configuration을 수정하자.  
  `image_id`와 `templatefile()`에 `server_text`를 전달할 것이다.

```tf
resource "aws_launch_configuration" "example" {
  image_id        = var.ami
  instance_type   = var.instance_type
  security_groups = [aws_security_group.instance.id]

  user_data = templatefile("${path.module}/user-data.sh", {
    server_port = var.server_port
    db_address  = data.terraform_remote_state.db.outputs.address
    db_port     = data.terraform_remote_state.db.outputs.port
    server_text = var.server_text
  })

  lifecycle {
    create_before_destroy = true
  }
}
```

- 이제 모듈을 불러와 사용하는 쪽에서 `ami`와 `server_text`를 전달해주자.

```tf
module "webserver_cluster" {
  source = "../../../modules/services/webserver-cluster"

  ami         = "ami-0fb653ca2d3203ac1"
  server_text = "New server text"

  cluster_name           = "webservers-prod"
  db_remote_state_bucket = "terraform-up-and-running-state-roy-ra"
  db_remote_state_key    = "prod/data-stores/mysql/terraform.tfstate"

  instance_type      = "t3.medium"
  min_size           = 2
  max_size           = 10
  enable_autoscaling = true

  custom_tags = {
    Owner     = "team-foo"
    ManagedBy = "terraform"
  }
}
```

- 여기서 `terraform apply`를 수행한 결과는 아래와 같이 두 개의 변경 사항이 생긴다.

  - (1) 기존 launch configuration을 새로운 `user_data`가 있는 launch configuration으로 수정
  - (2) ASG가 새로운 launch configuration을 사용하도록 수정

- 위의 두 가지 단계의 문제점은 새로운 launch configuration을 사용한다고 끝나는 것이 아니라, ASG가 새로운 EC2 instance들을  
  생성할 때까지 변경 사항이 반영되지 않는다는 것이다. 그렇다면 ASG에게 새로운 인스턴스들을 생성하라고 어떻게 알려줄 수 있을까?

- 하나의 선택지는 기존 ASG를 `terraform destroy` 등으로 제거하고, 다시 생성하는 것이다. 이 방식의 문제점은 ASG를 제거하는 순간  
  다운 타임이 발생한다는 것이다. 이 대신 _zero-downtime deployment_ 를 해야 한다. 이를 수행하는 하나의 방법은 새로운 ASG를  
  먼저 생성하고, 기존 ASG를 제거하는 것이다. 이전에 봤던 `create_before_destroy` lifecycle이 정확히 이를 수행한다.

- 이렇게 lifecycle을 설정해 zero-downtime deployment를 수행하려면 아래의 세 가지 단계가 필요하다.

  - (1) ASG의 `name` 파라미터가 launch configuration을 참조하도록 수정한다. Launch configuration이 변경될 때마다  
    이름이 바뀌므로 ASG의 이름도 바뀌고, 이는 Terraform이 ASG를 교체하도록 한다.

  - (2) ASG의 `create_before_destroy` 파라미터를 true로 바꿔 Terraform이 ASG를 교체하려 할 때마다 대체할 새로운  
    ASG를 먼저 생성하도록 한다.

  - (3) ASG의 `min_elb_capacity`를 클러스터의 `min_size`와 동일하게 설정해 Terraform이 새로운 ASG의 서버들이  
    ALB health check를 통과할 때까지 기존 ASG를 제거하지 않고 기다리도록 한다.

- 아래는 갱신된 `aws_autoscaling_group` 리소스이다.

```tf
resource "aws_autoscaling_group" "example" {
  name                 = "${var.cluster_name}-${aws_launch_configuration.example.name}"
  launch_configuration = aws_launch_configuration.example.name

  vpc_zone_identifier = data.aws_subnets.default.ids
  target_group_arns   = [aws_lb_target_group.asg.arn]
  health_check_type   = "ELB"

  min_size = var.min_size
  max_size = var.max_size

  min_elb_capacity = var.min_size

  lifecycle {
    create_before_destroy = true
  }

  tag {
    key                 = "Name"
    value               = var.cluster_name
    propagate_at_launch = true
  }

  dynamic "tag" {
    for_each = {
      for key, value in var.custom_tags :
      key => upper(value)
      if key != "Name"
    }

    content {
      key                 = tag.key
      value               = tag.value
      propagate_at_launch = true
    }
  }
}
```

- 이제 `terraform plan`을 수행하면 `aws_autoscaling_group`에 `# forces replacement`라는 문구가 생기는데,  
  이는 Terraform이 새로운 ASG로 이를 교체할 것임을 의미한다.

---

## Terraform Gotchas

- 지금까지 봤던 내용들 중, 한 단계 뒤로 물러나 단점들을 다시 봐야 할 필요성이 있다.

### `count` and `for_each` have limitations

- 이 장의 예제 코드들에서 우리는 `count`와 `for_each` 표현식을 loop와 if문에서 적극적으로 활용했다. 이는 잘 동작하지만,  
  반드시 알아두어야 할 중요한 한계점이 있다., 바로 `count`와 `for_each`에서는 resource output을 참조할 수 없다는 것이다.

- 우리가 여러 개의 EC2 instance들을 배포하고 싶고, 무언가의 이유로 ASG를 사용하지 않고 싶었다고 해보자. 코드는 아래와 같다.

```tf
resource "aws_instance" "example_1" {
  count         = 3
  ami           = "ami-0fb653ca2d3203ac1"
  instance_type = "t2.micro"
}
```

- `count`가 하드코딩된 값이기 때문에 위 코드는 문제없이 수행될 것이고, `apply`를 수행하면 3개의 EC2 instance가 생성될 것이다.  
  여기서 만약 EC2 instance를 현재 region의 AZ 당 하나씩 배포하려면 어떠헥 해야할까? 코드를 수정해 `aws_availability_zones`  
  data로 AZ들을 가져온 후, `count` 파라미터와 배열 참조를 통해 각 AZ를 순회하고, EC2 instance를 배포하면 된다.

```tf
data "aws_availability_zone" "all" {}

resource "aws_instance" "example_2" {
  count             = length(data.aws_availability_zones.all.names)
  availability_zone = data.aws_availability_zones.all.names[count.index]
  ami               = "ami-0fb653ca2d3203ac1"
  instance_type     = "t2.micro"
}
```

- 위 코드 또한 `count`가 data source를 문제 없이 참조할 수 있기 때문에 잘 동작한다. 하지만 만약 생성하고 싶은 인스턴스의 개수가  
  특정 리소스들의 output에 의해 결정된다면 어떨까? 이를 실험하기 가장 쉬운 방법은 정수 난수를 반환하는 `random_integer` 리소스를  
  사용하는 것이다.

```tf
resource "ranodm_integer" "num_instances" {
  min = 1
  max = 3
}
```

- 위 코드는 1과 3 사이의 정수 난수를 생성한다. 이제 이 리소스의 `result` output을 `count` 파라미터 내에 넣었을 때 어떻게 되는지 보자.

```tf
resource "aws_instance" "example_3" {
  count         = random_integer.num_instances.result
  ami           = "ami-0fb653ca2d3203ac1"
  instance_type = "t2.micro"
}
```

- 여기서 `terraform plan`을 수행하면 아래의 에러가 발생한다.

```
Error: Invalid count argument

  on main.tf line 30, in resource "aws_instance" "example_3":
  30: count     = random_integer.num_instances.result

  The "count" value depends on resource attributes that cannot be determined
  until apply, so Terraform cannot predict how many instances will be created.
  To work around this, use the -target argument to first apply only the
  resources that the count depends on.
```

- Terraform은 `plan` 단계에서, 즉 그 어떠한 리소스가 생성되거나 수정되기 전에 `count`와 `for_each`를 알아낼 수 있어야 함을 요구한다.  
  이는 곧 `count`와 `for_each`는 하드코딩된 값, 변수, data source, 그리고 심지어 원소들의 리스트는 참조할 수 있어도  
  리소스의 output은 참조할 수 없다는 것을 의미한다.

### Zero-downtime deployment has limitations

- 다운 타임 없는 배포를 위해 ASG에 `create_before_destroy` lifecycle을 사용할 때 유의해야 할 사항들이 몇 가지 있다.

- 첫 번째 문제는 auto scaling policy와는 잘 동작하지 않는다는 것이다. 더 정확하게 이야기하자면, ASG의 크기를 각 배포시마다  
  `min_size`로 초기화하는데, 이는 auto scaling policy를 사용해 실행되는 서버의 개수를 조정하고 있는 상황에 문제가 된다.  
  예를 들어 `webserver-cluster` 모듈은 `aws_autoscaling_schedule` 리소스를 2개 참조한다. 새롭게 배포한 ASG가  
  있다면 스케쥴과 상관없이 `min_size`인 2개의 서버만 배포될 것이다. 이를 회피하는 방법은 여러 가지 있는데, 예를 들어  
  `aws_autoscaling_schedule`의 `recurrence` 파라미터를 사용하거나, ASG의 `desired_capacity`를 사용할 수 있다.

- 하지만 더 심각한 두 번째 문제가 있는데, 바로 zero-downtime deployment와 같이 중요하고 복잡한 작업들에 대해서는  
  보통 `create_before_destroy`, `min_elb_capacity`, custom script 같은 난해한 방법 대신 최선의 해결책을 사용하고  
  싶어한다. 실제로 ASG에 대해 AWS는 이제 _instance refresh_ 라는 자체 기능을 지원한다.

- `aws_autoscaling_group`에 수행했던 zero-downtime deployment 관련 설정을 모두 제거하고, `instance_refresh` 블록을 추가하자.

```tf
resource "aws_autoscaling_group" "example" {
  name                 = var.cluster_name
  launch_configuration = aws_launch_configuration.example.name

  vpc_zone_identifier = data.aws_subnets.default.ids
  target_group_arns   = [aws_lb_target_group.asg.arn]
  health_check_type   = "ELB"

  min_size = var.min_size
  max_size = var.max_size

  instance_refresh {
    strategy = "Rolling"
    preferences {
      min_healthy_percentage = 50
    }
  }

  tag {
    key                 = "Name"
    value               = var.cluster_name
    propagate_at_launch = true
  }

  dynamic "tag" {
    for_each = {
      for key, value in var.custom_tags :
      key => upper(value)
      if key != "Name"
    }

    content {
      key                 = tag.key
      value               = tag.value
      propagate_at_launch = true
    }
  }
}
```

- 이제 ASG의 속성 값을 바꾸고 `apply`를 수행하면, 오직 `aws_launch_configuration`에만 변경 사항이 생기게 된다.  
  AWS는 새로운 인스턴스 하나를 먼저 띄우고, health check이 통과할 때까지 기다린 후, 이전 인스턴스 하나를 종료하고,  
  이 과정을 정해진 인스턴스 개수 만큼 반복한다.

- 이렇게 일반적인 경우, instance refresh와 같이 cloud provider가 직접 제공하는 가장 쉽고 대표적인 해결책을 사용하는 것이 좋다.

### Valid plans can fail

- 일부 경우 `plan`을 수행했을 때는 완벽히 올바른 결과가 주어짐에도 불구하고 `apply`를 수행했을 때 에러가 발생하는 경우가 있다.  
  예를 들어, `aws_iam_user` 리소스를 이전에 직접 만들었던 IAM user와 완전 일치하도록 추가해보자.

```tf
resource "aws_iam_user" "existing_user" {
  name = "sangwoo.ra"
}
```

- 이제 `plan`을 수행하면 새로운 IAM user 한 명이 추가될 것이라는 정상적인 결과가 출력된다.  
  하지만 `apply`를 하면 아래의 에러가 발생한다.

```
Error: Error while creating IAM User sangwoo.ra: EntityAlreadyExists:
User with name sangwoo.ra already exists.
```

- 문제는 당연히 해당 이름을 가진 IAM user가 이미 존재하기에 발생했다. 이는 IAM user 뿐만 아니라 대부분의 경우 모든 리소스에 대해  
  발생할 수 있다. 예를 들어 누군가가 해당 리소스를 직접 콘솔이나 CLI 명령을 통해 생성했고, 식별자가 동일해 충돌이 발생할 수 있다.  
  이런 리소스들은 Terraform의 state file에 저장되지 않기 때문에 `plan` 이 고려하지 않을 수 밖에 없다.

- 여기서 얻을 두 가지 교훈이 있다.

  - _After you start using Terraform, you should only use Terraform._

    - 인프라의 일부가 Terraform으로 관리되고 있다면, 이 리소스에 대해 직접 수정을 절대 하면 안된다. 이렇게 하지 않을 경우,  
      이상한 Terraform 에러들이 발생할 수 있으며, IaC의 많은 장점들을 잃어버리게 될 수 있다.

  - _If you have existing infrastructure, use the `import` command._

    - Terraform을 사용하기 전에 이미 만들어진 인프라가 있다면, `terraform import` 명령으로 해당 인프라를 Terraform의  
      state file로 불러와 Terraform이 이를 알고 관리할 수 있도록 할 수 있다. `import` 명령은 두 개의 인자를 받는다.  
      첫 번째 인자는 Terraform configuration file에 해당 리소스의 "주소"이다. 이는 `<PROVIDER>_<TYPE>.<NAME>`과  
      같은 리소스를 참조하도록 한다. 두 번째 인자는 리소스의 고유 ID이다. 예를 들어, `aws_iam_user` 리소스의 경우, 고유 ID는  
      user의 이름이다. 위에서 본 문제를 해결하기 위한 `import` 명령은 `terraform import aws_iam_user.existing_user sangwoo.ra`이다.

    - Terraform은 AWS API를 사용해 IAM user를 찾고, 발견된 IAM user와 state file 사이의 관계를 만들어 해당 IAM user가  
      Terraform 코드의 `aws_iam_user.existing_user`와 연결되도록 한다.

    - 만약 이미 수많은 리소스들이 존재하고, 이들을 Terraform으로 관리하고 싶을 때 처음부터 Terraform 코드를 하나씩 작성하고  
      각 리소스에 대해 `terraform import`를 수행하는 것이 꽤나 번거로울 수 있다. 이를 위해 terraformer와 terracognita를  
      사용하면 클라우드 환경의 리소스에 대한 Terraform 코드와 state file을 자동으로 생성할 수 있다.

### Refactoring can be tricky

- 일반적인 프로그래밍 관습 중 리팩토링이 있는데, 리팩토링을 하면 가독성을 향상시키고, 유지보수성, 그리고 코드의 전반적인 품질을  
  높힐 수 있다. 하지만 Terraform 혹은 다른 IaC 도구들에서는 코드 조각의 "외부 행동"이 어떻게 정의되는지 신경쓰지 않으면  
  예상치 못한 문제들에 봉착할 수 있다.

- 예를 들어, 일반적인 리팩토링 방법 중 하나는 변수 또는 함수명을 바꾸는 것이다. GPL에서 이는 매우 쉽게 수행할 수 있지만,  
  Terraform에서는 이에 대해 굉장히 조심해야 한다.

- 예를 들어, `webserver-cluster` module에는 아래의 `cluster_name`이라는 input variable이 있다.

```tf
variable "cluster_name" {
  description = "The name to use for all the cluster resources"
  type        = string
}
```

- 위 클러스터 이름을 처음엔 foo로 지정했다가, bar로 바꾸고 싶은 상황이 생겼다고 해보자. 작은 변경 사항처럼 보일 수 있지만,  
  실제로는 장애를 발생시킬 수도 있다.

- 이는 `webserver-cluster` module이 `cluster_name` 변수를 많은 리소스에서 참조하기 때문이다.  
  특정 리소스들의 `name` 파라미터를 변경하면 Terraform은 리소스의 기존 버전을 삭제하고, 새로운 버전을 생성해 교체할 것이다.  
  만약 삭제할 리소스가 ALB가 되어버리면, 새로운 ALB가 생성되기 전까지 트래픽을 서버로 전달할 방법이 없어 다운 타임이 발생하게 된다.

- 또다른 리팩토링으로 Terraform identifier를 바꾸고 싶을 수 있다. 예를 들어, `webserver-cluster` module의 아래  
  `aws_security_group` 리소스를 생각해보자.

```tf
resource "aws_security_group" "instance" {
  # ..
}
```

- 위 리소스의 identifier는 instance이다. 이 이름을 cluster_instance로 변경하고 싶다고 해보자.

```tf
resource "aws_security_group" "cluster_instance" {
  # ..
}
```

- `apply` 결과는 예상한대로 다운 타임을 발생시킨다.

- Terraform은 cloud provider의 identifier와 Terraform 리소스 identifier와 연관짔기 때문에, 리소스 identifier를  
  변경하면 Terraform 입장에서는 기존 리소스는 완전히 제거하고, 새로운 리소스가 생겨버린 꼴이 된다.

- 여기서 얻을 수 있는 4가지 교훈이 있다.

  - _Always use the `plan` command_

    - 이런 유의사항들은 `plan`을 통해 모두 확인할 수 있다. 항상 `apply` 전에는 `plan`을 수행해 결과를 살펴보자.

  - _Create before destroy_

    - 만약 리소스를 교체하고 싶다면, 기존 것을 삭제하기 전에 대체될 리소스가 먼저 만들어져야 하는 상황인지 항상 생각해봐야 한다.  
      만약 그렇다면, `create_before_destroy`를 사용하면 된다. 또한 직접 새로운 리소스 코드를 작성하고 `apply`한 후,  
      기존 리소스 코드를 제거하고 또 `apply`하는 방법도 있다.

  - _Refactoring may require changing state_

    - 실수로 다운 타임이 발생하지 않게 하면서 코드를 리팩토링하려면 코드에 맞게 Terraform state를 갱신해줘야 한다.  
      하지만 절대 Terraform state를 직접 수정하면 안된다! 대신 두 가지 선택지가 있다. 하나는 `terraform state mv` 명령으로  
      수동 삭제하는 것이고, 다른 하나는 코드에 `moved` block을 추가하는 것이다.

    - 먼저 `terraform state mv` 명령을 보자. `terraform state mv <ORIGINAL_REFERENCE> <NEW_REFERENCE>`의  
      형식을 가지며, `ORIGINAL_REFERENCE`는 기존 리소스, `NEW_REFERENCE`는 state를 이동시키고 싶은 새로운 리소스를 나타낸다.  
      위의 예시처럼 `aws_security_group`의 identifier를 instance에서 cluster_instance로 변경하고 싶다면,  
      아래처럼 실행하면 된다.

      ```sh
      terraform state mv \
        aws_security_group.instance \
        aws_security_group.cluster_instance
      ```

    - 위 명령은 `aws_security_group.instance`와 연관된 state들이 `aws_security_group.cluster_instance`로  
      이동하도록 한다. 그리고 `terraform plan`을 실행하면 변경 사항이 없다고 나오게 된다.

    - 이러한 CLI 명령을 수동으로 실행하는 것은 휴먼 에러를 발생시키기 쉬우며, 코드베이스 클 수록 그 가능성도 높아진다.  
      이를 위해 Terraform 1.1은 `moved` block이라는 기능을 만들었다. 코드를 리팩토링할 때마다 state가 어떻게 갱신되어야  
      하는지를 나타내는 `moved` block을 추가해야 한다. 예를 들어 `aws_security_group`리소스가 instance에서  
      cluster_instance로 이름이 바뀌었음을 나타내기 위해선 아래처럼 코드를 작성하면 된다.

      ```tf
      moved {
        from = aws_security_group.instance
        to   = aws_security_group.cluster_instance
      }
      ```

    - 이제 `apply`를 할 때마다 Terraform이 자동으로 state file을 갱신해야 하는지 여부를 확인하게 된다.  
      `apply` 결과는 아래와 같다.

      ```
      # aws_security_group.instance has moved to
      # aws_security_group.cluster_instance
        resource "aws_security_group" "cluster_instance" {
          name = "moved-example-security-group"
          tags = {}
          # (8 unchanged attributes hidden)
        }

      Plan: 0 to add, 0 to change, 0 to destroy.
      ```

    - yes를 입력하면 Terraform은 state file을 자동으로 갱신한다.

  - _Some parameters are immutable_

    - 리소스의 많은 파라미터들은 불변성을 가지기 때문에 만약 수정하면 Terraform은 기존 리소스를 삭제하고, 새로운 리소스를 생성해  
      교체하게 된다. 마찬가지로 꼭 `plan`을 사용하고, `create_before_destroy`를 사용할지 여부를 생각해야 한다.

---
