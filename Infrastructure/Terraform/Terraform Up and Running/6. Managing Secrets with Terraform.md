# Managing Secrets with Terraform

- 데이터베이스 비밀번호, API Key, TLS 인증서 등 다양한 비밀 값이 포함되어 있는 상황이 발생할 수 있다.  
  예를 들어, 아래처럼 데이터베이스를 배포하기 위한 Terraform 코드가 있다고 해보자.

```tf
resource "aws_db_instance" "example" {
  identifier_prefix   = "terraform-up-and-running"
  engine              = "mysql"
  allocated_storage   = 10
  instance_class      = "db.t2.micro"
  skip_final_snapshot = true
  db_name             = var.db_name

  username = "???"
  password = "???"
}
```

- 위 코드는 username과 password, 2개의 비밀 값을 사용한다. 이러한 값들을 어떻게 안전하게 관리할까?

- 이 부분은 _secrets management_ 의 넓은 주제이며, 아래의 세 가지를 다룰 것이다.

  - Secret management basics
  - Secret management tools
  - Secret management tools with Terraform

## Secret management basics

- 가장 중요한 점은 **비밀 값을 plain text로 하드코딩하지 않는 것** 이다. 아래처럼 하지 말라는 것이다.

```tf
resource "aws_db_instance" "example" {
  identifier_prefix   = "terraform-up-and-running"
  engine              = "mysql"
  allocated_storage   = 10
  instance_class      = "db.t2.micro"
  skip_final_snapshot = true
  db_name             = var.db_name

  username = "admin"
  password = "password"
}
```

- Plain text로 비밀 값을 저장하는 것은 좋지 못한 생각이다. 아래는 그 이유 중 일부이다.

  - VCS에 접근 권한이 있는 누구나 비밀 값 또한 접근할 수 있다.
  - VCS에 접근 가능한 어떠한 컴퓨터도 비밀 값에 접근할 수 있다.
  - 실행되고 있는 소프트웨어들이 비밀 값에 접근할 수 있다.
  - 비밀 값의 접근 추적을 하거나 권한 회수가 불가능하다.

---

## Secret management tools

- Terraform과 함께 비밀 값들을 관리하기 위한 도구를 사용하려면 아래의 주제들을 알아두는 것이 좋다.

  - 저장할 수 있는 비밀 값들의 종류
  - 비밀 값들을 저장하는 방식
  - 비밀 값에 접근하기 위해 사용하는 인터페이스
  - Secret management tool들의 비교

### 저장할 수 있는 비밀 값들의 종류

- 비밀 값들은 크게 세 가지 종류로 분류된다.

  - Personal secrets: 개인에게 속하는 비밀 값으로, 웹사이트를 사용하기 위한 아이디와 비밀번호 등이 있다.
  - Customer secrets: 고객에게 속하는 비밀 값으로, 고객이 우리의 웹사이트에 접속하기 위해 사용하는 아이디와 비밀번호 등이 있다.
  - Infrastructure secrets: 인프라에 속하는 비밀 값으로, 데이터베이스 비밀번호 등이 있다.

- 대부분의 secret management tool들은 위 세가지 중 하나를 관리하기 위한 목적이 있으며, 그 목적에 부합하게 사용하는 것이 올바르다.

### 비밀 값들을 저장하는 방식

- 비밀 값들을 저장하는 방법은 크게 두 가지가 있는데, 하나는 file-based secret store이고, 다른 하나는 centralized secret store이다.

- _File-based secret store_ 는 비밀 값들을 암호화된 파일들에 저장하고, 이들은 일반적으로 VCS에서 검사가 이뤄진다.  
  파일을 암호화하기 위해서는 암호 key가 필요하며, 이 암호 key 자체 또한 비밀값이다. 그러다면 암호 key는 어떻게 관리할 것인가?

- 위의 문제에 대한 일반적인 해결책은 cloud provider가 제공하는 _KMS(Key Management Service)_ 를 사용하는 것이다.  
  이들은 모두 cloud provider를 신뢰해 cloud provider가 안전하게 비밀 값들을 관리하고, 접근 제어도 하도록 한다.  
  또다른 방법은 PGP Key(Pretty Good Privacy Key)를 사용하는 것인데, 이는 public key와 private key로 구성된다.

- _Centralized secret store_ 는 일반적으로 웹 서비스의 형태로 제공되며, 네트워크로 상호 작용하며 비밀 값들을 MySQL, PostgreSQL 등의  
  데이터베이스에 저장한다. 비밀 값들을 암호화하기 위해 centralized store들은 암호화 key를 필요로한다. 일반적으로 이 암호화 key는  
  서비스 자체에 의해 관리되거나, KMS를 사용하여 관리된다.

### 비밀 값에 접근하기 위해 사용하는 인터페이스

- 대부분의 secret management tool들은 API, CLI 혹은 다른 UI를 통해 접근할 수 있다.

- Centralized secret store들이 제공하는 이러한 API는 코드가 프로그래밍적으로 비밀 값들을 읽어와 사용할 때 유용하다.

- 모든 file-based secret store는 CLI를 기반으로 동작한다. CLI는 개발자들이 비밀 값에 접근하기 위한 편리한 수단이다.

- Centralized secret store 중 일부는 웹, 데스크톱 혹은 모바일 UI도 제공한다.

### Secret management tool들의 비교

- 아래 그림은 유명한 secret management tool들을 세 가지 유의 사항을 기반으로 비교한 것이다.

  ![picture 1](/images/TFRU_20.png)
  ![picture 2](/images/TFRU_21.png)

---

## Secret management tools with Terraform

- 이제 secret management tool들을 Terraform과 함께 사용하는 방법을 알아보자.  
  아래는 Terraform 코드가 비밀 값을 참조할만한 부분이다.

  - Providers
  - Resource and data sources
  - State files and plan files

### Providers

- 일반적으로 Terraform 코드를 사용할 때 가장 먼저 비밀 값을 사용하게 될 부분은 provider와 인증하는 과정일 것이다.  
  예를 들어 AWS provider를 사용할 때 `terraform apply`를 하려면, 먼저 AWS와 인증을 할 수 있어야 한다.  
  인증에 필요한 access key들을 어떻게 관리할까? 그리고 이들을 Terraform에게 어떻게 전달할까?

- 위 질문에 대한 해답은 굉장히 많다. 우선 아래처럼 하드코딩하는 것은 당연히 옳지 않다.

```tf
provider "aws" {
  region = "us-east-2"

  access_key = (AWS_ACCESS_KEY_ID)
  secret_key = (AWS_SECRET_ACCESS_KEY)
}
```

- 위처럼 하지 않고 안전하게 Terraform에게 인증 관련 키들을 전달하는 방법은 크게 두가지가 있다.  
  아래 종류는 Terraform을 사용하는 주체에 따라 나눈 것이다.

  - Human users: Terraform을 컴퓨터에서 사용하는 개발자들
  - Machine users: Terraform을 사용하는 자동화된 시스템(CI server 등)

#### Human users

- 모든 Terraform provider들은 credential을 코드에 직접 넣는 방법 외에도 다양한 수단을 제공한다.  
  가장 일반적인 방법은 환경 변수로써 인증 정보를 전달하는 것이다.

- `export`로 환경 변수를 저장해 사용하는 것은 코드에 plain text로 비밀 값들을 저장하지 않아도 되게 하고, Terraform을  
  사용하는 그 누구도 자신만의 credential을 제공하도록 하며, 메모리에만 인증 정보가 저장되도록 보장한다.

- 한 가지 중요한 의문은 애초에 인증 정보들이 어디에 저장되어야 하냐는 것이다. 이들은 외우기엔 너무 길고 복잡하고, 만약 컴퓨터 내에  
  plain text로 저장하면 보안에 취약해진다. 이를 위해 1Password, LastPass, 또는 직접 터미널로 복사 붙여넣기할 수 있다.

> CLI에서 인증 정보를 자주 사용한다면, CLI를 제공하는 secret manager를 활용하자.

- AWS의 경우 `aws-vault`를 활용하면 더욱 편리하다. 아래처럼 할 수 있다.

  ```sh
  $ aws-vault add dev
    Enter Access Key Id: (AWS_ACCESS_KEY_ID)
    Enter Secret Key: (AWS_SECRET_ACCESS_KEY)
  $ aws-vault exec dev -- terraform apply
  ```

#### Machine users

- 자동화된 시스템이 Terraform을 활용하는 경우에는 CircleCI의 경우엔 CircleCI Context를 활용하고, EC2의 경우  
  IAM role을 할당하고, Github actions의 경우에는 OIDC를 사용하는 등 상황에 알맞게 인증 정보를 주입하도록 하면 된다.

### Resource and data sources

- Terraform 코드에서 비밀 값을 사용할 수 있는 두 번째 장소는 리소스와 data source이다.  
  예를 들어, 위에서 봤던 데이터베이스 관련 코드를 다시 보자.

```tf
resource "aws_db_instance" "example" {
  identifier_prefix   = "terraform-up-and-running"
  engine              = "mysql"
  allocated_storage   = 10
  instance_class      = "db.t2.micro"
  skip_final_snapshot = true
  db_name             = var.db_name

  username = "admin"
  password = "password"
}
```

- 위 처럼 코드에 plain text로 비밀 값을 지정하지 않는 대신 사용할 수 있는 방법으로 다음 세 가지가 있다.

  - Environment variables
  - Encrypted files
  - Secret stores

#### Environment variables

- 첫 번째 방식으로는 provider 인증 과정과 마찬가지로 환경 변수를 사용하는 것이다.  
  이를 위해서는 아래처럼 환경 변수로 주입할 비밀 값들을 variable로 선언해야 한다.

```tf
variable "db_username" {
  description = "The username for the database"
  type        = string
  sensitive   = true
}

variable "db_password" {
  description = "The password for the database"
  type        = string
  sensitive   = true
}
```

- 이전에 봤듯이 위의 변수들의 `sensitive` 속성이 true로 설정되어 있기 때문에, Terraform은 `plan`과 `apply` 시 위 값들을  
  출력하지 않을 것이다. 그리고 `sensitive`가 true인 변수들에는 `default`를 지정하지 말아야 한다.

- 데이터베이스 관련 코드가 변수를 참조하도록 바꿔주자.

```tf
resource "aws_db_instance" "example" {
  identifier_prefix   = "terraform-up-and-running"
  engine              = "mysql"
  allocated_storage   = 10
  instance_class      = "db.t2.micro"
  skip_final_snapshot = true
  db_name             = var.db_name

  username = var.db_username
  password = var.db_password
}
```

- 이제 아래와 같이 `TF_VAR_`를 prefix로 붙여 환경 변수로 주입해 전달할 수 있다.

```sh
export TF_VAR_db_username=(DB_USERNAME)
export TF_VAR_db_password=(DB_PASSWORD)
```

- 환경 변수에 들어갈 값 자체를 저장하기 위해서는 secret management tool들을 활용하면 된다.

- 환경 변수를 사용하는 데에는 아래의 장점들이 있다.

  - 코드와 VCS에 plain text로 하드코딩된 비밀 값들이 없다.
  - 비밀 값들을 저장하기 쉽다. 왜냐하면 거의 대부분의 secret management tool들이 CLI를 지원하기 때문이다.
  - 비밀 값을 필요할 때 알아내기도 쉽다.
  - 자동화 테스트와 통합하기 쉽다.
  - 비용이 소모되지 않는다.

- 반면, 단점들은 아래와 같다.

  - 모든 것이 Terraform 코드 자체에 있지는 않다. 이는 코드를 이해하고 유지보수하기 어렵게 만든다. 해당 코드를 사용하는 모든 사용자가  
    Terraform을 사용하기 위해 환경 변수를 수동으로 일일히 지정하거나 별도의 script를 실행해야만 한다.

  - Secret management를 위한 정책을 표준화하는 것은 어렵다. 비밀 값들의 관리가 Terraform 외부에서 이뤄지기 때문에,  
    코드에서 비밀 값들의 관리 방법을 강제할 수도 없다.

  - 비밀 값들이 versioning, packaging, 그리고 코드와 함께 테스트되고 있지 않기 때문에, 설정 오류들이 쉽게 발생할 수 있다.

#### Encrypted files

- 두 번째 방법은 비밀값들을 암호화하고, 파일에 해당 암호문을 저장하고, 해당 파일을 VCS에 checking하는 것이다.

- 이전에 봤듯이 파일을 암호화하기 위한 별도의 암호화 key가 필요하며, 이는 cloud provider의 KMS를 활용해 구축할 수 있다.

- AWS KMS를 사용하는 예시를 보자. 우선 KMS CMK(Customer Managed Key)를 생성하기 위해 key policy를 정의해 어떤 사용자가  
  해당 CMK를 사용할 수 있는지 지정하자. 간단하게 예제를 가져가기 위해, 현재 사용자에게 CMK의 관리자 권한을 할당하는 코드를 보자.

```tf
provider "aws" {
  region = "us-east-2"
}

data "aws_caller_identity" "self" {}

data "aws_iam_policy_document" "cmk_admin_policy" {
  statement {
    effect    = "allow"
    resources = ["*"]
    actions   = ["kms:*"]
    principals {
      type        = "AWS"
      identifiers = [data.aws_caller_identity.self.arn]
    }
  }
}

resource "aws_kms_key" "cmk" {
  policy = data.aws_iam_policy_document.cmk_admin_policy.json
}

resource "aws_kms_alias" "cmk" {
  name          = "alias/kms-cmk-example"
  target_key_id = aws_kms_key.cmk.id
}
```

- 다음으로 비밀 값들을 포함하고 있는 `db-creds.yml` 파일을 생성해보자.

```yml
username: admin
password: password
```

- 위 파일은 암호화기 되어 있지 않기에 VCS에 check하면 안된다. 이를 암호화하려면 `aws kms encrypt` 명령을 사용해야 한다.

- 아래는 파일을 암호화기 위한 간단한 bash script인 `encrypt.sh`이다.

```sh
#!/bin/bash

CMK_ID="$1"
AWS_REGION="$2"
INPUT_FILE="$3"
OUTPUT_FILE="$4"

echo "Encrypting contents of $INPUT_FILE using CMK $CMK_ID..."
ciphertext=$(aws kms encrypt \
	--key-id "$CMK_ID" \
	--region "$AWS_REGION" \
	--plaintext "fileb://$INPUT_FILE" \
	--output text \
	--query CiphertextBlob)

echo "Writing result to $OUTPUT_FILE..."
echo "$ciphertext" > "$OUTPUT_FILE"

echo "Done!"
```

- 위 스크립트는 아래처럼 사용할 수 있다.

```sh
$ ./encrypt.sh \
    alias/kms-cmk-example \
    us-east-2 \
    db-creds.yml \
    db-creds.yml.encrypted
```

- 위의 암호화된 `db-creds.yml.encrypted` 파일은 이제 VCS에 올려도 무방하고, 사용할 때는 우선 아래처럼  
  `aws_kms_secrets` data source를 사용해 복호화하고, `yamldecode()` built-in function을 통해 YAML 파일에서 원하는 값을  
  꺼내올 수 있다.

```tf
data "aws_kms_secrets" "creds" {
  secret {
    name    = "db"
    payload = file("${path.module}/db-creds.yml.encrypted")
  }
}

locals = {
  db_creds = yamldecode(data.aws_kms_secrets.creds.plaintext["db"])
}

resource "aws_db_instance" "example" {
  identifier_prefix   = "terraform-up-and-running"
  engine              = "mysql"
  allocated_storage   = 10
  instance_class      = "db.t2.micro"
  skip_final_snapshot = true
  db_name             = var.db_name

  username = local.db_creds.username
  password = local.db_creds.password
}
```

- 이렇게 암호화된 파일로 비밀 값을 관리하는 것은 아래의 장점들이 있다.

  - 코드와 VCS에서 plain text로 하드코딩된 비밀 값들이 없다.
  - 비밀 값들이 VCS에 암호화되어 보관되기 때문에 versioning, packaging, 그리고 나머지 코드와 함께 테스트가 가능하다.  
    이로써 설정 오류를 방지할 수 있다.
  - 비밀값을 가져오기가 쉽다.
  - AWS KMS, GCP KMS, PGP 등 다양한 암호화 방식과 잘 동작한다.
  - 모든 것이 코드에 정의되어 있다. 수동적으로 해야할 일이 없다.

- 단점들은 아래와 같다.

  - 비밀값을 저장하는 과정이 복잡하다. 수많은 명령을 수행하거나 sops와 같은 외부 도구를 사용해야 한다.
  - 자동화 테스트와 통합하기가 더욱 어렵다.
  - 비밀값들이 암호화되어 VCS에 저장되기 때문에 비밀 값을 rotate하거나 회수하기가 까다롭다.
  - 비밀 값의 사용 내역을 추적하기 어렵다.
  - 비밀 값 관리를 위한 정책을 표준화하기 어렵다.

#### Secret stores

- 마지막은 비밀값들을 centralized secret store에 저장하는 방법이다.

- 유명한 secret store로는 AWS Secrets Manager, Google Secret Manager, Azure Key Vault 등이 있다.  
  AWS Management Console에 접속해 AWS Secrets Manager로 이동해, JSON format에 맞게 비밀 값을 저장한다.  
  그리고 이제 아래처럼 data source를 활용해 비밀 값을 가져올 수 있다.

```tf
data "aws_secretsmanager_secret_version" "creds" {
  secret_id = "db-creds"
}

locals {
  db_creds = jsonencode(
    data.aws_secretsmanager_secret_version.creds.secret_string
  )
}

resource "aws_db_instance" "example2" {
  identifier_prefix   = "terraform-up-and-running"
  engine              = "mysql"
  allocated_storage   = 10
  instance_class      = "db.t2.micro"
  skip_final_snapshot = true
  db_name             = var.db_name

  username = local.db_creds.username
  password = local.db_creds.password
}
```

- Secret store를 사용하는 것은 아래의 장점들이 있다.

  - 코드에 비밀 값들이 하드코딩되지 않는다.
  - 모든 것이 코드에 정의되어 있다.
  - 비밀 값을 저장하기 쉽다.
  - Secret store가 자체적으로 비밀 값을 rotate하거나 회수하는 기능을 지원한다.
  - Secret store가 자체적으로 audit log를 제공한다.
  - 비밀 값 저장 정책을 표준화하기 쉽다.

- 반면 아래의 단점들이 있다.

  - 비밀 값들이 versioning, packaging, 테스트되지 않기에 설정 오류가 발생할 수 있다.
  - 대부분의 managed secret store는 비용이 발생한다.
  - 비밀 값을 가져오기가 더욱 어렵다.
  - 통합 테스트와 연동하기가 어렵다.

### State files and plan files

- 마지막으로, Terraform을 사용할 때 비밀 값을 사용하게 될 부분이 아래 두 가지가 있다.

  - State files
  - Plan files

#### State files

- 이제 비밀 값을 코드에 하드코딩하지 않아야 하는 이유와 그 필요성에 대해 충분히 알았다.  
  하지만 Terraform은 한 가지 큰 문제점이 있는데, 바로 **Terraform 리소스 또는 data source에 보내는 모든 비밀 값들이**  
  **Terraform state file에 저장된다** 는 것이다.

- 예를 들어, 데이터베이스 인증 정보를 어디서 가져오는지와 무관하게, Terraform은 해당 비밀 값들을 `terraform.tfstate` 파일에  
  저장할 것이다. 이는 2014년부터 대두된 문제였지만, 아직까지도 선명한 해결책이 존재하지 않는다. 따라서 비밀 값을 어떻게 관리하느냐와  
  무관하게 아래의 내용들을 꼭 지켜야 한다.

  - Store Terraform state in a backend that supports encryption: Terraform state를 로컬에 저장하거나  
    VCS에서 관리하는 대신, Terraform이 지원하는 backend들 중 암호화를 자체적으로 지원하는 backend를 사용해야 한다.  
    여기에는 S3, GCS, Azure Blob Storage 등이 있다.

  - Strictly control who can access your Terraform backend: Terraform state file은 비밀 값을 가지고 있을 수  
    있기 때문에, 비밀 값을 관리하는 것과 마찬가지의 수준으로 관리해야 한다.

#### Plan files

- 지금까지 `terraform plan`을 많이 봐왔다. 아직 보지 못한 기능 중 하나로 `plan`의 결과를 파일에 담는 기능이 있다.

  ```sh
  $ terraform plan -out=example.plan
  ```

- 그리고 위에 나온 plan 결과를 토대로 apply할 수도 있다.

  ```sh
  $ terraform apply example.plan
  ```

- 이 기능은 편리할 수 있지만, **state 파일과 마찬가지로 plan output 파일도 비밀 값들을 담고 있게 된다** 는 큰 문제점에 유의해야 한다.  
  따라서 plan 파일을 활용할 때는 아래 사항들을 꼭 지켜야 한다.

  - Encrypt your Terraform plan files
  - Strictly control who can access your Terraform plan files

---
